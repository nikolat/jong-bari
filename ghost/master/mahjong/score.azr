//ロン
string Menu_Ron(dict ref)
{
	int nAgariPlayer = ref["Reference2"];
	int nFurikomiPlayer = ref["Reference3"];
	string hai13 = arTehai[nAgariPlayer];
	string sute = ref["Reference4"];
	return showScoreResult(nAgariPlayer, nFurikomiPlayer, hai13, sute, {});
}

//ツモ
string Menu_Tsumo(dict ref)
{
	int nAgariPlayer = ref["Reference2"];
	int nFurikomiPlayer = -1;
	string hai13 = arTehai[nAgariPlayer];
	string tsumo = arYama[nYamaIndex - 1];
	return showScoreResult(nAgariPlayer, nFurikomiPlayer, hai13, tsumo, {});
}

//流局
string OnRyukyokuScore(dict ref)
{
	array arTenpaiPlayerFlag = _strary(ref["Reference0"]);
	return showScoreResult(-1, -1, {}, "", arTenpaiPlayerFlag);
}

//点数画面表示
string showScoreResult(int nAgariPlayer, int nFurikomiPlayer, string hai13, string atariHai, array arTenpaiPlayerFlag)
{
	string s;
	s += "\![set,balloontimeout,0]\![set,choicetimeout,0]\0\s[0]\b[2]\_q";
	//王牌表示
	array wanpaiOmote;
	array wanpaiUra;
	array arDorahyouji = strHaiToArray(dorahyouji);
	string uradorahyouji;
	for (int i = 0; arDorahyouji[i] != nil; i++)
	{
		uradorahyouji += arYama[59 + i];
	}
	array arUradorahyouji = strHaiToArray(uradorahyouji);
	for (int i = 0; i < 5; i++)
	{
		if (arDorahyouji[4 - i] == nil)
		{
			wanpaiOmote += "tb";
			wanpaiUra += "tb";
		}
		else
		{
			wanpaiOmote += arDorahyouji[4 - i];
			wanpaiUra += arUradorahyouji[4 - i];
		}
	}
	wanpaiOmote += "tb";
	wanpaiOmote += "tb";
	wanpaiUra += "tb";
	wanpaiUra += "tb";
	s += "\f[height,+16]\_l[110,]";
	for (int i = 0; wanpaiOmote[i] != nil; i++)
	{
		s += convertUnicode(wanpaiOmote[i]);
	}
	s += "\n\_l[110,]";
	for (int i = 0; wanpaiUra[i] != nil; i++)
	{
		s += convertUnicode(wanpaiUra[i]);
	}
	s += "\f[height,default]\n\n";
	s += "\_l[215,0] " + arBafu[bafu] + kyoku + "局";
	s += "\_l[260,0]●x" + tsumibou + "\n";
	s += "\_l[260,]\f[color,red]●\f[color,default]x" + kyotaku;
	s += "\_l[0,80]";
	//役・点数表示
	int score;
	int nFu;
	dict dYakuman;
	dict dYakuAndHan;
	if (nAgariPlayer >= 0)
	{
		string bafuHai = getBafuHai();
		string jifuHai = getJifuHai(nAgariPlayer);
		int isRichi = 0;
		if (arRichiJunme[nAgariPlayer] >= 0)
			isRichi = 1;
		string strDora;
		for (int i = 0; arDorahyouji[i] != nil; i++)
		{
			strDora += getDoraFromDorahyouji(arDorahyouji[i]);
			if (isRichi)
				strDora += getDoraFromDorahyouji(arUradorahyouji[i]);
		}
		int isTsumo = 0;
		if (nFurikomiPlayer == -1)
			isTsumo = 1;
		int isWRichi = 0;
		if (arRichiJunme[nAgariPlayer] == 0)
			isWRichi = 1;
		int isIppatsu = 0;
		if (isRichi && arRichiJunme[nAgariPlayer] == _aryvn(arKawa[nAgariPlayer]) - 1)//鳴きを考慮していないため注意
			isIppatsu = 1;
		int is1stRound = 0;
		if (_aryvn(arKawa[nAgariPlayer]) == 0)//鳴きを考慮していないため注意
			is1stRound = 1;
		int isFinalTileWin = 0;
		if (_aryvn(arYama) == nYamaIndex)
			isFinalTileWin = 1;
		int isRinshan = isRinshanChance;
		int isChankan = 0;
		int isOya = 0;
		if (nAgariPlayer == oyaIndex)
			isOya = 1;
		dict d = _saorirequest("MahjongUtil", "score", hai13, atariHai, bafuHai, jifuHai, strDora, isTsumo, isRichi, isWRichi, isIppatsu, is1stRound, isFinalTileWin, isRinshan, isChankan, isOya);
		score = d["Result"];
		int isYakuman = 0;
		if (score >= 32000 && !isOya || score >= 48000 && isOya)
			isYakuman = 1;
		int nHan;
		if (isYakuman)
		{
			for (int i = 1; d["Value" + i] != nil; i++)
			{
				array a = _strsplit(d["Value" + i], ",");
				dYakuman += $(a[0], (int)a[1]);
			}
		}
		else
		{
			nFu = d["Value0"];
			nHan = d["Value1"];
			for (int i = 2; d["Value" + i] != nil; i++)
			{
				array a = _strsplit(d["Value" + i], ",");
				dYakuAndHan += $(a[0], (int)a[1]);
			}
		}
		array arYakuman = _dickeyget(dYakuman);
		for (int i = 0; arYakuman[i] != nil; i++)
		{
			s += arYakuman[i] + "\_l[100,]役満\n";
		}
		if (_aryvn(arYakuman) == 0)
		{
			array arYaku = _dickeyget(dYakuAndHan);
			for (int i = 0; arYaku[i] != nil; i++)
			{
				s += _strreplace(arYaku[i], "ヤオ", "么") + "\_l[100,]" + dYakuAndHan[arYaku[i]] + "翻\n";
			}
		}
		if (nHan > 0)
		{
			s += "" + nHan + "翻 " + nFu + "符\n";
		}
		if (score > 0)
			s += "" + score + "点\n";
		s += "\n";
	}
	//プレイヤー持ち点表示
	int nKyotaku;
	if (nAgariPlayer >= 0)
	{
		nKyotaku = kyotaku;
		kyotaku = 0;
	}
	array arScoreAdd = getScoreAdd(nAgariPlayer, nFurikomiPlayer, score, tsumibou, nKyotaku, arTenpaiPlayerFlag);
	for (int i = 0; playerName[i] != nil; i++)
	{
		arScore[i] += arScoreAdd[i];
		s += "<<" + playerName[i] + ">>\_l[100,]" + arScoreAdd[i] + "\_l[160,]" + arScore[i] + "\n";
	}
	s += "\n";
	//牌表示
	s += "\f[height,+16]";
	s += showHai(hai13, atariHai, nAgariPlayer, 0, 1, 1);
	s += "\n\f[height,default]\n";
	s += "\![*]\__q[Menu_StartKyoku]次へ\__q";
	s += " \![*]\__q[Menu_CANCEL]閉じる\__q";
	//通知
	if (nAgariPlayer >= 0)
	{
		s += "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion()
			+ ",agari," + playerName[nAgariPlayer] + "," + nFu;
		array arYaku = _dickeyget(dYakuAndHan);
		for (int i = 0; arYaku[i] != nil; i++)
		{
			s += ",\"" + arYaku[i] + "," + dYakuAndHan[arYaku[i]] + "\"";
		}
		s += "]";
	}
	else
	{
		s += "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion()
			+ ",ryukyoku]";
	}
	for (int i = 0; i < 3; i++)
	{
		string fugo = "";
		if (arScoreAdd[i] > 0)
			fugo = "+";
		else if (arScoreAdd[i] < 0)
			fugo = "-";
		if (fugo != "")
			s += "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion()
				+ ",point," + playerName[i] + "," + fugo + "," + (int)_fabs(arScoreAdd[i]) + "]";
	}
	s += "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion()
		+ ",kyokuend]";
	s += "\_q\e";
	//連荘判定
	if (nAgariPlayer == oyaIndex)
	{
		tsumibou++;
	}
	else
	{
		if (nAgariPlayer >= 0)
			tsumibou = 0;
		else
			tsumibou++;
		kyoku++;
		if (kyoku == 5)
		{
			kyoku = 1;
			bafu++;
		}
		oyaIndex++;
		if (oyaIndex == 4)
			oyaIndex = 0;
	}
	clearStatus();
	return s;
}

string getBafuHai()
{
	string bafuHai = "1z";
	if (bafu == 1)
		bafuHai = "2z";
	return bafuHai;
}

string getJifuHai(int nPlayer)
{
	array seki = {"1z", "2z", "3z", "4z"};
	dict dSeki;
	array pNames;
	for (int i = 0; playerName[i] != nil; i++)
	{
		pNames += playerName[(i + oyaIndex) % 4];
	}
	for (int i = 0; pNames[i] != nil; i++)
	{
		dSeki += $(pNames[i], seki[i]);
	}
	return dSeki[playerName[nPlayer]];
}

array getScoreAdd(int nAgariPlayer, int nFurikomiPlayer, int score, int nTsumibou, int nKyotaku, array arTenpaiPlayerFlag)
{
	array arScoreAdd = {0, 0, 0, 0};
	if (_aryvn(arTenpaiPlayerFlag) == 0)
	{
		if (nFurikomiPlayer >= 0)
		{
			arScoreAdd[nFurikomiPlayer] = -1 * (score + (300 * nTsumibou));
			arScoreAdd[nAgariPlayer] = score + (300 * nTsumibou) + (1000 * nKyotaku);
		}
		else
		{
			for (int i = 0; i < 4; i++)
			{
				if (nAgariPlayer == i)
				{
					if (nAgariPlayer == oyaIndex)
					{
						int nShou = (int)(score / 300) * 100;
						int nAmari = score % 300;
						int nScore = nShou;
						if (nAmari > 0)
							nScore += 100;
						nScore = 3 * nScore;
						arScoreAdd[i] = nScore + (300 * nTsumibou) + (1000 * nKyotaku);
					}
					else
					{
						int nShou1 = (int)(score / 200) * 100;
						int nAmari1 = score % 200;
						int nScore1 = nShou1;
						if (nAmari1 > 0)
							nScore1 += 100;
						int nShou2 = (int)(score / 400) * 100;
						int nAmari2 = score % 400;
						int nScore2 = nShou2;
						if (nAmari2 > 0)
							nScore2 += 100;
						int nScore = nScore1 + (2 * nScore2);
						arScoreAdd[i] = nScore + (300 * nTsumibou) + (1000 * nKyotaku);
					}
				}
				else
				{
					if (nAgariPlayer == oyaIndex)
					{
						int nShou = (int)(score / 300) * 100;
						int nAmari = score % 300;
						int nScore = nShou;
						if (nAmari > 0)
							nScore += 100;
						arScoreAdd[i] = -1 * (nScore + (100 * nTsumibou));
					}
					else
					{
						if (i == oyaIndex)
						{
							int nShou = (int)(score / 200) * 100;
							int nAmari = score % 200;
							int nScore = nShou;
							if (nAmari > 0)
								nScore += 100;
							arScoreAdd[i] = -1 * (nScore + (100 * nTsumibou));
						}
						else
						{
							int nShou = (int)(score / 400) * 100;
							int nAmari = score % 400;
							int nScore = nShou;
							if (nAmari > 0)
								nScore += 100;
							arScoreAdd[i] = -1 * (nScore + (100 * nTsumibou));
						}
					}
				}
			}
		}
	}
	else
	{
		int nTenpai;
		for (int i = 0; arTenpaiPlayerFlag[i] != nil; i++)
		{
			nTenpai += arTenpaiPlayerFlag[i];
		}
		int plus;
		int minus;
		if (nTenpai == 0 || nTenpai == 4)
		{
			plus = 0;
			minus = 0;
		}
		else
		{
			plus = 3000 / nTenpai;
			minus = -3000 / (4 - nTenpai);
		}
		for (int i = 0; arTenpaiPlayerFlag[i] != nil; i++)
		{
			if (arTenpaiPlayerFlag[i])
				arScoreAdd[i] = plus;
			else
				arScoreAdd[i] = minus;
		}
	}
	return arScoreAdd;
}

string getDoraFromDorahyouji(string hai)
{
	dict d = ${
		$("1m", "2m"),
		$("2m", "3m"),
		$("3m", "4m"),
		$("4m", "5m"),
		$("5m", "6m"),
		$("6m", "7m"),
		$("7m", "8m"),
		$("8m", "9m"),
		$("9m", "1m"),
		$("1p", "2p"),
		$("2p", "3p"),
		$("3p", "4p"),
		$("4p", "5p"),
		$("5p", "6p"),
		$("6p", "7p"),
		$("7p", "8p"),
		$("8p", "9p"),
		$("9p", "1p"),
		$("1s", "2s"),
		$("2s", "3s"),
		$("3s", "4s"),
		$("4s", "5s"),
		$("5s", "6s"),
		$("6s", "7s"),
		$("7s", "8s"),
		$("8s", "9s"),
		$("9s", "1s"),
		$("1z", "2z"),
		$("2z", "3z"),
		$("3z", "4z"),
		$("4z", "1z"),
		$("5z", "6z"),
		$("6z", "7z"),
		$("7z", "5z")
	};
	return d[hai];
}
