//本来は他家の捨て牌とか諸々の情報が必要
string naniwokiru(string hai, array kawa, string bafuHai, string jifuHai, string dorahyouji)
{
	string s = getDahai(hai, kawa, bafuHai, jifuHai, dorahyouji);
	if (s == "")
	{
		dAdditionalHeader = ${
			$("ErrorLevel", "warning"),
			$("ErrorDescription", "failed \"naniwokiru\" with " + hai)
		};
	}
	return s;
}

//テンパイに近づける
string getDahai(string hai, array kawa, string bafuHai, string jifuHai, string dorahyouji)
{
	array arDahai;
	int point = -1;
	//副露を除く
	array hai14 = strHaiToArrayWithoutFuro(hai);
	//ドラ
	array arDorahyouji = strHaiToArray(dorahyouji);
	array arDora;
	for (int i = 0; arDorahyouji[i] != nil; i++)
	{
		arDora += getDoraFromDorahyouji(arDorahyouji[i]);
	}
	//孤立牌
	array arKoritsuhai = getKoritsuHai(hai14);
	for (int i = 0; hai14[i] != nil; i++)
	{
		if (ASEARCH(hai14[i], arDahai) == -1)
		{
			array a = getShantenWithMentsu(removeHai(hai, hai14[i]), bafuHai, jifuHai);
			int shanten13 = a[0];
			array arPattern = a[1];
			int shantenPoint = 10000 * (10 - shanten13);
			int elementMaxPoint = 0;
			for (int j = 0; arPattern[j] != nil; j++)
			{
				int elementPoint = 0;
				if (arPattern[j] == "chitoitsu" || arPattern[j] == "kokushimusou")
				{
					elementPoint = 0;
				}
				else
				{
					array ap = _strsplit(arPattern[j], ",");
					array arTatsu13 = {};
					if (ap[0] != "")
					{
						elementPoint += 40;//雀頭
					}
					for (int k = 1; ap[k] != nil; k++)
					{
						if (_strlen(ap[k]) == 6)
						{
							elementPoint += 90;//面子
						}
						else if (_strlen(ap[k]) == 4)
						{
							arTatsu13 += ap[k];
						}
					}
					for (int k = 0; arTatsu13[k] != nil; k++)
					{
						array arTatsuNum = convertNumArray(strHaiToArray(arTatsu13[k]));
						if ((arTatsuNum[0] != 1) && (arTatsuNum[0] + 1 == arTatsuNum[1]) && (arTatsuNum[1] != 9))
							elementPoint += 60;//両面
						else if (arTatsuNum[0] == arTatsuNum[1])
							elementPoint += 40;//対子
						else if ((arTatsuNum[0] == 1) && (arTatsuNum[1] == 2) || (arTatsuNum[0] == 8) && (arTatsuNum[1] == 9))
							elementPoint += 20;//辺張
						else
							elementPoint += 30;//嵌張
					}
				}
				if (elementMaxPoint < elementPoint)
					elementMaxPoint = elementPoint;
			}
			//孤立牌を優先的に切る
			int koritsuPoint = 0;
			if (ASEARCH(hai14[i], arKoritsuhai) >= 0)
			{
				koritsuPoint = 500;
			}
			//既に捨てた牌を優先的に切る
			int furitenPoint = 0;
			if (ASEARCH(hai14[i], kawa) >= 0)
			{
				furitenPoint = 10;
			}
			//ドラは残しておきたい
			int doraPoint = 0;
			if (ASEARCH(hai14[i], arDora) >= 0)
			{
				doraPoint = -50;
			}
			int dahaiPoint = shantenPoint + elementMaxPoint + koritsuPoint + furitenPoint;
			if (point < dahaiPoint)
			{
				point = dahaiPoint;
				arDahai = {hai14[i]};
			}
			else if (point == dahaiPoint)
			{
				arDahai += hai14[i];
			}
		}
	}
	return _randselect(arDahai);
}

//孤立牌の探索
array getKoritsuHai(array hai)
{
	array arRet;
	for (int i = 0; hai[i] != nil; i++)
	{
		int num = _substr(hai[i], 0, 1);
		string color = _substr(hai[i], 1, 1);
		if (color == "m" || color == "p" || color == "s")
		{
			string haiP2 = "" + (num - 2) + color;
			string haiP1 = "" + (num - 1) + color;
			string haiN1 = "" + (num + 1) + color;
			string haiN2 = "" + (num + 2) + color;
			if (!(ASEARCH(haiP2, hai) >= 0
				|| ASEARCH(haiP1, hai) >= 0
				|| _aryvn(ASEARCHEX(hai[i], hai)) > 1
				|| ASEARCH(haiN1, hai) >= 0
				|| ASEARCH(haiN2, hai) >= 0))
			{
				arRet += hai[i];
			}
		}
		else if (color == "z")
		{
			if (_aryvn(ASEARCHEX(hai[i], hai)) == 1)
			{
				arRet += hai[i];
			}
		}
	}
	return arRet;
}

array convertNumArray(array hai)
{
	array arNum;
	for (int i = 0; hai[i] != nil; i++)
	{
		arNum += (int)_substr(hai[i], 0, 1);
	}
	return arNum;
}
