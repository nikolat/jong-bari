//******************************************************************************
// 設定
//******************************************************************************
//手牌オープン
int openTehai = 0;

//******************************************************************************
// グローバル変数
//******************************************************************************
//ゲームで使用する変数
array arYama;
array arTehai = {"", "", "", ""};
array arKawa = {{}, {}, {}, {}};
array arFuritenCheckRichi = {{}, {}, {}, {}};
array arFuritenCheckTurn = {{}, {}, {}, {}};
array arScore = {25000, 25000, 25000, 25000};
array arRichiJunme = {-1, -1, -1, -1};
array arFuroJunme = {{}, {}, {}, {}};
array arFuroHistory = {{}, {}, {}, {}};
string dorahyouji;
int nYamaIndex;
int isGameStarted = 0;
int bafu = 0;//東:0, 南:1
array arBafu = {"東", "南"};
int kyoku = 0;//1 - 4
int oyaIndex = -1;
int tsumibou;
int kyotaku;
array playerName;
int isRinshanChance;
int isSoloMode;
//バルーン表示保存用
string lastScript;
//クライアントからのレスポンス保存用
string reservedSelectedHai;
int reservedRichi;
int reservedTsumo;
int reservedKakan;
int reservedAnkan;
dict reservedNaku;
dict reservedTenpai;
string reservedScript;
dict dResponseNeed;
int isRecievedResponse;

//******************************************************************************
// OnMahjongResponse
//******************************************************************************
string OnMahjongResponse(dict ref)
{
	string version = ref["Reference0"];
	if (version != getVersion())
		return "";
	string command = ref["Reference1"];
	switch (command)
	{
	case "hello":
		array a = _strsplit(ref["Reference3"], "=");
		string name;
		if (a[0] == "name")
			name = a[1];
		else
			return "";
		if (_aryvn(playerName) >= 3)
			return "";//定員オーバー
		if (ASEARCH(name, playerName) >= 0)
			return "";//同名のキャラが参加済
		playerName += name;
		if (_aryvn(playerName) == 3)
		{
			playerName += "user";
			return showBalloon();
		}
		else
		{
			return "";
		}
		break;
	case "sutehai?":
		string action = ref["Reference2"];
		switch (action)
		{
		case ""://お任せモード
			break;
		case "sutehai":
			reservedSelectedHai = ref["Reference3"];
			break;
		case "richi":
			reservedSelectedHai = ref["Reference3"];
			reservedRichi = 1;
			break;
		case "tsumo":
			reservedTsumo = 1;
			break;
		case "kakan":
			reservedSelectedHai = ref["Reference3"];
			reservedKakan = 1;
			break;
		case "ankan":
			reservedSelectedHai = ref["Reference3"];
			reservedAnkan = 1;
			break;
		default:
			break;
		}
		isRecievedResponse = 1;
		break;
	case "naku?":
		string name = ref["Sender"];
		reservedNaku[name] = {ref["Reference2"], ref["Reference3"], ref["Reference4"]};
		dResponseNeed[name] = 0;
		int count = 0;
		for (int i = 0; i < 3; i++)
		{
			count += dResponseNeed[playerName[i]];
		}
		if (count == 0)
			isRecievedResponse = 1;
		break;
	case "tenpai?":
		string action = ref["Reference2"];
		string name = ref["Sender"];
		reservedTenpai[name] = action;
		dResponseNeed[name] = 0;
		int count = 0;
		for (int i = 0; i < 3; i++)
		{
			count += dResponseNeed[playerName[i]];
		}
		if (count == 0)
			isRecievedResponse = 1;
		break;
	default:
		break;
	}
	return "";
}

TH_WaitResponse()
{
	double wait = 0.125;
	int nLoop = 8;
	for (int i = 0; i < nLoop; i++)
	{
		if (isSoloMode)
			break;
		_sleep(wait);
		if (isRecievedResponse)
		{
			isRecievedResponse = 0;
			break;
		}
	}
	string s = reservedScript;
	reservedScript = "";
	_speak(s);
}

string getVersion()
{
	return "UKAJONG/0.2";
}

//******************************************************************************
// 表示
//******************************************************************************
string showBalloon()
{
	//接続確認
	if (_aryvn(playerName) < 3)
	{
		string s;
		s += "\0\s[0]\_q";
		s += "\![*]\__q[Menu_SearchPlayer]対局相手を探す\__q\n";
		s += "\![*]\__q[Menu_StartWithoutPlayer]対局相手無しで始める\__q\n\n";
		s += "\![*]\__q[Menu_CANSEL]閉じる\__q\_q\e";
		return s;
	}
	//gamestart通知
	if (!isGameStarted)
	{
		isGameStarted = 1;
		bafu = 0;
		kyoku = 1;
		oyaIndex = _randselect({0, 1, 2, 3});
		array seki = {"東", "南", "西", "北"};
		dict dSeki;
		array pNames;
		for (int i = 0; playerName[i] != nil; i++)
		{
			pNames += playerName[(i + oyaIndex) % 4];
		}
		for (int i = 0; pNames[i] != nil; i++)
		{
			dSeki += $(pNames[i], seki[i]);
		}
		string s;
		s += "\0\s[0]";
		for (int i = 0; i < 3; i++)
		{
			s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
				+ ",gamestart," + dSeki[playerName[i]] + "," + pNames[0] + "," + pNames[1] + "," + pNames[2] + "," + pNames[3] + "]";
		}
		s += "\_w[2000]\![raise,showBalloon]\e";
		return s;
	}
	string s;
	if (nYamaIndex > 0)
		s += lastScript;
	else
		s += startKyoku();
	return s;
}

string Menu_SearchPlayer(dict ref)
{
	isSoloMode = 0;
	return "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion() + ",hello]";
}
string Menu_StartWithoutPlayer(dict ref)
{
	isSoloMode = 1;
	playerName = {"エミリ", "テディ", "エミリオ", "user"};
	isGameStarted = 1;
	bafu = 0;
	kyoku = 1;
	oyaIndex = _randselect({0, 1, 2, 3});
	return startKyoku();
}

string Menu_StartKyoku(dict ref)
{
	if (bafu < 2)//東場、南場
		return startKyoku();
	else
		return endGame();
}

string endGame()
{
	//gameend通知
	string s = "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion() + ",gameend";
	for (int i = 0; playerName[i] != nil; i++)
	{
		s += "," + playerName[i] + _bytechar(1) + arScore[i];
	}
	s += "]\e";
	isGameStarted = 0;
	playerName = {};
	arScore = {25000, 25000, 25000, 25000};
	return s;
}

string startKyoku()
{
	arYama = shuffleArray(getAllHai());
	arTehai[0] = _arystr(sortHai(getArrayRange(arYama, 0, 12)));
	arTehai[1] = _arystr(sortHai(getArrayRange(arYama, 13, 25)));
	arTehai[2] = _arystr(sortHai(getArrayRange(arYama, 26, 38)));
	arTehai[3] = _arystr(sortHai(getArrayRange(arYama, 39, 51)));
	arKawa = {{}, {}, {}, {}};
	arFuritenCheckRichi = {{}, {}, {}, {}};
	arFuritenCheckTurn = {{}, {}, {}, {}};
	arRichiJunme = {-1, -1, -1, -1};
	arFuroJunme = {{}, {}, {}, {}};
	arFuroHistory = {{}, {}, {}, {}};
	isRinshanChance = 0;
	dorahyouji = arYama[52];
	nYamaIndex = 66;//王牌14枚(from 52 to 65)抜く
	reservedNaku = ${
		$(playerName[0], {}),
		$(playerName[1], {}),
		$(playerName[2], {})
	};
	reservedTenpai = ${
		$(playerName[0], ""),
		$(playerName[1], ""),
		$(playerName[2], "")
	};
	dResponseNeed = ${
		$(playerName[0], 0),
		$(playerName[1], 0),
		$(playerName[2], 0)
	};
	string tsumo = arYama[nYamaIndex++];
	string s;
	//kyokustart通知
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",kyokustart," + arBafu[bafu] + "," + playerName[oyaIndex] + "," + tsumibou + "," + kyotaku + "]";
	//haipai通知
	for (int i = 0; i < 3; i++)
	{
		s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
			+ ",haipai," + playerName[i] + "," + arTehai[i] + "]";
	}
	//dora通知
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",dora," + dorahyouji + "]";

	s += "\![set,balloontimeout,0]\0\s[0]\b[2]\_q";
	s += showStatus(tsumo, "", oyaIndex, -1, {0, 0, 0});
	//userがツモ
	if (oyaIndex == 3)
	{
		s += "\_q\e";
		lastScript = s;
		return s;
	}
	//user以外がツモ
	s += "\![raiseother," + playerName[oyaIndex] + ",OnMahjong," + getVersion()
		+ ",tsumo," + playerName[oyaIndex] + "," + getLeftYama() + "," + tsumo + "]";
	s += "\![raiseother," + playerName[oyaIndex] + ",OnMahjong," + getVersion()
		+ ",sutehai?]";
	s += "\_q\e";
	reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + oyaIndex + "," + tsumo + "]";
	_create_thread("TH_WaitResponse");
	return s;
}

int getLeftYama()
{
	return _aryvn(arYama) - nYamaIndex;
}

string OnSuteSelect(dict ref)
{
	int selectPlayer = ref["Reference0"];
	string tsumo = ref["Reference1"];
	string s = "\![set,balloontimeout,0]\0\s[0]\b[2]\_q";
	string sute;
	string tehai = addHai(arTehai[selectPlayer], tsumo);
	string wait = "";
	if (reservedTsumo)
	{
		reservedTsumo = 0;
		s += showStatus(tsumo, "", selectPlayer, selectPlayer, {0, 0, 0});
		//発声通知
		s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
			+ ",say," + playerName[selectPlayer] + ",tsumo]";
		s += "\x\![raise,OnChoiceSelectEx,,Menu_Tsumo," + selectPlayer + "]\e";
		return s;
	}
	else if (reservedSelectedHai != "")
	{
		if (reservedRichi && canRichi(selectPlayer, tsumo))
		{
			sute = reservedSelectedHai;
			reservedSelectedHai = "";
			arRichiJunme[selectPlayer] = _aryvn(arKawa[selectPlayer]);
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",richi]";
			wait += "\_w[2000]";
		}
		else if (reservedAnkan && canAnkan(selectPlayer, tsumo))
		{
			reservedAnkan = 0;
			string ankanHai = reservedSelectedHai;
			reservedSelectedHai = "";
			arTehai[selectPlayer] = addHai(arTehai[selectPlayer], tsumo);
			isRinshanChance = 1;
			setAnkan(selectPlayer, ankanHai);
			string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
			dorahyouji += strDorahyoujiNew;
			tsumo = arYama[nYamaIndex++];
			s += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, 0);
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",kan]";
			s += "\_w[2000]";
			//晒した牌通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",open," + playerName[selectPlayer] + "," + ankanHai + ankanHai + ankanHai + ankanHai + "]";
			//ツモ通知
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",tsumo," + playerName[selectPlayer] + "," + getLeftYama() + "," + tsumo + "]";
			//dora通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",dora," + strDorahyoujiNew + "]";
			//捨て牌問い合わせ
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",sutehai?]";
			s += "\_q\e";
			reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]";
			_create_thread("TH_WaitResponse");
			return s;
		}
		else if (reservedKakan && canKakan(selectPlayer, tsumo))
		{
			reservedKakan = 0;
			string kakanHai = reservedSelectedHai;
			reservedSelectedHai = "";
			arTehai[selectPlayer] = addHai(arTehai[selectPlayer], tsumo);
			isRinshanChance = 1;
			setKakan(selectPlayer, kakanHai);
			string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
			dorahyouji += strDorahyoujiNew;
			tsumo = arYama[nYamaIndex++];
			s += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, 1);
			//この時点でロン(槍槓)を受け付ける必要があるんだけどどうしよう
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",kan]";
			s += "\_w[2000]";
			//晒した牌通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",open," + playerName[selectPlayer] + "," + kakanHai + kakanHai + kakanHai + kakanHai + "]";
			//ツモ通知
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",tsumo," + playerName[selectPlayer] + "," + getLeftYama() + "," + tsumo + "]";
			//捨て牌問い合わせ
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",sutehai?]";
			//dora通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",dora," + strDorahyoujiNew + "]";
			s += "\_q\e";
			reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]";
			_create_thread("TH_WaitResponse");
			return s;
		}
		reservedRichi = 0;
		reservedAnkan = 0;
		reservedKakan = 0;
		if (sute == "")
		{
			//指定されたアクションが不可能だった
			dAdditionalHeader = ${
				$("ErrorLevel", "warning"),
				$("ErrorDescription", "failed requested action")
			};
			return "\0Error\e";
		}
	}
	else if(tsumo != "")//auto カン以外の副露直後を除く
	{
		if (canTsumo(selectPlayer, tsumo))
		{
			s += showStatus(tsumo, "", selectPlayer, selectPlayer, {0, 0, 0});
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",tsumo]";
			s += "\x\![raise,OnChoiceSelectEx,,Menu_Tsumo," + selectPlayer + "]\e";
			return s;
		}
		if (shouldAnkan(selectPlayer, tsumo))
		{
			arTehai[selectPlayer] = addHai(arTehai[selectPlayer], tsumo);
			array arAnkanHaiUseful = getAnkanHaiUseful(arTehai[selectPlayer], selectPlayer);
			string ankanHai = _randselect(arAnkanHaiUseful);
			isRinshanChance = 1;
			setAnkan(selectPlayer, ankanHai);
			string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
			dorahyouji += strDorahyoujiNew;
			tsumo = arYama[nYamaIndex++];
			s += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, 0);
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",kan]";
			s += "\_w[2000]";
			//晒した牌通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",open," + playerName[selectPlayer] + "," + ankanHai + ankanHai + ankanHai + ankanHai + "]";
			//ツモ通知
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",tsumo," + playerName[selectPlayer] + "," + getLeftYama() + "," + tsumo + "]";
			//dora通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",dora," + strDorahyoujiNew + "]";
			//捨て牌問い合わせ
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",sutehai?]";
			s += "\_q\e";
			reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]";
			_create_thread("TH_WaitResponse");
			return s;
		}
		else if (shouldKakan(selectPlayer, tsumo))
		{
			arTehai[selectPlayer] = addHai(arTehai[selectPlayer], tsumo);
			array arKakanHaiUseful = getKakanHaiUseful(arTehai[selectPlayer], selectPlayer);
			string kakanHai = _randselect(arKakanHaiUseful);
			isRinshanChance = 1;
			setKakan(selectPlayer, kakanHai);
			string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
			dorahyouji += strDorahyoujiNew;
			tsumo = arYama[nYamaIndex++];
			s += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, 1);
			//この時点でロン(槍槓)を受け付ける必要があるんだけどどうしよう
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",kan]";
			s += "\_w[2000]";
			//晒した牌通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",open," + playerName[selectPlayer] + "," + kakanHai + kakanHai + kakanHai + kakanHai + "]";
			//ツモ通知
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",tsumo," + playerName[selectPlayer] + "," + getLeftYama() + "," + tsumo + "]";
			//捨て牌問い合わせ
			s += "\![raiseother," + playerName[selectPlayer] + ",OnMahjong," + getVersion()
				+ ",sutehai?]";
			//dora通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",dora," + strDorahyoujiNew + "]";
			s += "\_q\e";
			reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]";
			_create_thread("TH_WaitResponse");
			return s;
		}
		else if (canRichi(selectPlayer, tsumo))
		{
			arRichiJunme[selectPlayer] = _aryvn(arKawa[selectPlayer]);
			sute = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji);
			//発声通知
			s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[selectPlayer] + ",richi]";
			wait += "\_w[2000]";
		}
		else
		{
			if (arRichiJunme[selectPlayer] >= 0)
				sute = tsumo;
			else
				sute = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji);
		}
	}
	else//auto カン以外の副露直後
	{
		sute = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji);
	}
	isRinshanChance = 0;
	setSutehai(sute, selectPlayer);
	arTehai[selectPlayer] = removeHai(tehai, sute);
	s += showStatus("", sute, selectPlayer, -1, {0, 0, 0});
	//捨て牌通知
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",sutehai," + playerName[selectPlayer] + "," + sute + "]";
	//userが鳴く、ロンするのが可能な場合、\xを挟んで一時停止する canPonはcanDaiminkanを兼ねる
	if (canRon(3, sute) || canPon(3, sute) || (selectPlayer + 1 == 3) && canChi(3, sute))
		s += "\x[noclear]";
	isRecievedResponse = 1;
	for (int i = 0; playerName[i] != nil; i++)
	{
		if (i != selectPlayer)
		{
			array command = {};
			if (canRon(i, sute))
				command += "ron";
			if (canPon(i, sute))
				command += "pon";
			if (selectPlayer + 1 == i && canChi(i, sute))
				command += "chi";
			if (canDaiminkan(i, sute))
				command += "kan";
			if (_aryvn(command) > 0)
			{
				//鳴くか問い合わせ
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",naku?," + sute + "," + JOIN(command, ",") + "]";
				dResponseNeed[playerName[i]] = 1;
				isRecievedResponse = 0;
			}
		}
	}
	s += "\![notify,OnNotifyPrepareSuteComplete," + selectPlayer + "," + sute + "]";
	s += "\_q\e";
	return s;
}

int canPon(int nPlayer, string suteHai)
{
	if (arRichiJunme[nPlayer] >= 0)
		return 0;
	if (_aryvn(arYama) == nYamaIndex)
		return 0;
	if (_aryvn(ASEARCHEX(suteHai, strHaiToArrayWithoutFuro(arTehai[nPlayer]))) >= 2)
		return 1;
	return 0;
}

int shouldPon(int nPlayer, string suteHai)
{
	if (!canPon(nPlayer, suteHai))
		return 0;
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	string hai13 = arTehai[nPlayer];
	string hai14 = removeHai(hai13, suteHai + suteHai) + "<" + suteHai + suteHai + suteHai + ">";
	int shantenBefore = getShantenYaku(hai13, bafuHai, jifuHai);
	int shantenAfter = getShantenYaku(hai14, bafuHai, jifuHai);
	if (shantenAfter < shantenBefore)
		return 1;
	return 0;
}

int canChi(int nPlayer, string suteHai)
{
	if (arRichiJunme[nPlayer] >= 0)
		return 0;
	if (_aryvn(arYama) == nYamaIndex)
		return 0;
	array a = getChiMaterial(arTehai[nPlayer], suteHai);
	if (_aryvn(a) > 0)
		return 1;
	return 0;	
}

int shouldChi(int nPlayer, string suteHai)
{
	if (!canChi(nPlayer, suteHai))
		return 0;
	array a = getChiMaterial(arTehai[nPlayer], suteHai);
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	string hai13 = arTehai[nPlayer];
	int shantenBefore = getShantenYaku(hai13, bafuHai, jifuHai);
	for (int i = 0; a[i] != nil; i++)
	{
		string hai14 = removeHai(hai13, a[i]) + "<" + _arystr(sortHai(strHaiToArray(a[i] + suteHai))) + ">";
		int shantenAfter = getShantenYaku(hai14, bafuHai, jifuHai);
		if (shantenAfter < shantenBefore)
			return 1;
	}
	return 0;
}

array getChiMaterial(string strTehai, string suteHai)
{
	int n = _substr(suteHai, 0, 1);
	string color = _substr(suteHai, 1, 1);
	if (color == "z")
		return {};
	array arTehaiPlayer = strHaiToArrayWithoutFuro(strTehai);
	array a;
	for (int i = 0; arTehaiPlayer[i] != nil; i++)
	{
		if (_substr(arTehaiPlayer[i], 1, 1) == color)
			a += (int)_substr(arTehaiPlayer[i], 0, 1);
	}
	array arRet;
	if (ASEARCH(n - 2, a) >= 0 && ASEARCH(n - 1, a) >= 0)
		arRet += "" + (n - 2) + color + (n - 1) + color;
	if (ASEARCH(n - 1, a) >= 0 && ASEARCH(n + 1, a) >= 0)
		arRet += "" + (n - 1) + color + (n + 1) + color;
	if (ASEARCH(n + 1, a) >= 0 && ASEARCH(n + 2, a) >= 0)
		arRet += "" + (n + 1) + color + (n + 2) + color;
	return arRet;
}

int canDaiminkan(int nPlayer, string suteHai)
{
	if (arRichiJunme[nPlayer] >= 0)
		return 0;
	if (_aryvn(arYama) == nYamaIndex)
		return 0;
	if (_aryvn(ASEARCHEX(suteHai, strHaiToArrayWithoutFuro(arTehai[nPlayer]))) >= 3)
		return 1;
	return 0;
}

int shouldDaiminkan(int nPlayer, string suteHai)
{
	if (!canDaiminkan(nPlayer, suteHai))
		return 0;
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	string hai13 = arTehai[nPlayer];
	string hai14 = removeHai(hai13, suteHai + suteHai + suteHai) + "<" + suteHai + suteHai + suteHai + suteHai + ">";
	int shantenBefore = getShantenYaku(hai13, bafuHai, jifuHai);
	int shantenAfter = getShantenYaku(hai14, bafuHai, jifuHai);
	if (shantenAfter <= shantenBefore)
		return (int)_randselect(0, 1);
	return 0;
}

int canRon(int nPlayer, string atariHai)
{
	//和了かどうか(シャンテン数が-1かどうか)検証する
	string hai14 = addHai(arTehai[nPlayer], atariHai);
	int shanten = getShanten(hai14);
	if (shanten != -1)
		return 0;
	//フリテンかどうか検証する
	dict d = _saorirequest("MahjongUtil", "machi", arTehai[nPlayer]);
	if (d["head"] == "SAORI/1.0 400 Bad Request")
	{
		dAdditionalHeader = ${
			$("ErrorLevel", "warning"),
			$("ErrorDescription", "failed \"machi\" in \"canRon\" with " + arTehai[nPlayer])
		};
		return 0;
	}
	array arMachi = strHaiToArray(d["Result"]);
	int isRichi = 0;
	if (arRichiJunme[nPlayer] >= 0)
		isRichi = 1;
	for (int i = 0; arMachi[i] != nil; i++)
	{
		string strMachi = arMachi[i];
		if (ASEARCH(strMachi, arKawa[nPlayer]) >= 0)
			return 0;
		if (isRichi)
		{
			int index = ASEARCH(strMachi, arFuritenCheckRichi[nPlayer]);
			if (index >= 0 && index != _aryvn(arFuritenCheckRichi[nPlayer]) - 1)
				return 0;
		}
		if (ASEARCH(strMachi, arFuritenCheckTurn[nPlayer]) >= 0)
		{
			int index = ASEARCH(strMachi, arFuritenCheckTurn[nPlayer]);
			if (index >= 0 && index != _aryvn(arFuritenCheckTurn[nPlayer]) - 1)
				return 0;
		}
	}
	//役があるかどうか検証する
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	string strDora = "";
	int isTsumo = 0;
	int isIppatsu = 0;
	int isChankan = 0;
	int isRinshan = 0;
	int isFinalTileWin = 0;
	if (_aryvn(arYama) == nYamaIndex)
		isFinalTileWin = 1;
	int is1stRound = 0;
	if (_aryvn(arKawa[nPlayer]) == 0)//鳴きを考慮していないため注意
		is1stRound = 1;
	dict d = _saorirequest("MahjongUtil", "score", arTehai[nPlayer], atariHai, bafuHai, jifuHai, strDora, isTsumo, isRichi, isIppatsu, isChankan, isRinshan, isFinalTileWin, is1stRound);
	if (d["head"] == "SAORI/1.0 400 Bad Request")
	{
		dAdditionalHeader = ${
			$("ErrorLevel", "warning"),
			$("ErrorDescription", "failed \"score\" in \"canRon\" with " + arTehai[nPlayer] + " " + atariHai)
		};
		return 0;
	}
	int score = d["Result"];
	if (score <= 0)
		return 0;
	return 1;
}

int canTsumo(int nPlayer, string atariHai)
{
	if (atariHai == "")
		return 0;
	//和了かどうか(シャンテン数が-1かどうか)検証する
	string hai14 = addHai(arTehai[nPlayer], atariHai);
	int shanten = getShanten(hai14);
	if (shanten != -1)
		return 0;
	//役があるかどうか検証する
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	string strDora = "";
	int isTsumo = 1;
	int isRichi = 0;
	int isIppatsu = 0;
	int isChankan = 0;
	int isRinshan = isRinshanChance;
	int isFinalTileWin = 0;
	if (_aryvn(arYama) == nYamaIndex)
		isFinalTileWin = 1;
	int is1stRound = 0;
	dict d = _saorirequest("MahjongUtil", "score", arTehai[nPlayer], atariHai, bafuHai, jifuHai, strDora, isTsumo, isRichi, isIppatsu, isChankan, isRinshan, isFinalTileWin, is1stRound);
	if (d["head"] == "SAORI/1.0 400 Bad Request")
	{
		dAdditionalHeader = ${
			$("ErrorLevel", "warning"),
			$("ErrorDescription", "failed \"score\" in \"canTsumo\" with " + arTehai[nPlayer] + " " + atariHai)
		};
		return 0;
	}
	int score = d["Result"];
	if (score <= 0)
		return 0;
	return 1;
}

int canRichi(int nPlayer, string tsumoHai)
{
	int shanten = getShanten(addHai(arTehai[nPlayer], tsumoHai));
	if (shanten <= 0 && arRichiJunme[nPlayer] == -1 && isMenzenPlayer(nPlayer) && getLeftYama() >= 4)
		return 1;
	return 0;
}

int canKakan(int nPlayer, string tsumoHai)
{
	array arKakanHai = getKakanHai(addHai(arTehai[nPlayer], tsumoHai));
	if (_aryvn(arKakanHai) > 0)
		return 1;
	return 0;
}

int shouldKakan(int nPlayer, string tsumoHai)
{
	string tehai = addHai(arTehai[nPlayer], tsumoHai);
	array arKakanHaiUseful = getKakanHaiUseful(tehai, nPlayer);
	if (_aryvn(arKakanHaiUseful) > 0)
		return 1;
	return 0;
}

array getKakanHai(string hai)
{
	array arHai = strHaiToArrayWithFuro(hai);
	array arHaiWithoutFuro;
	array arMinko;
	array arRet;
	for (int i = 0; arHai[i] != nil; i++)
	{
		if (arHai[i][0] == "normal")
			arHaiWithoutFuro += arHai[i][1];
		else if (arHai[i][0] == "furo" && _strlen(arHai[i][1]) == 6 && _substr(arHai[i][1], 0, 2) == _substr(arHai[i][1], 2, 2))
			arMinko += _substr(arHai[i][1], 0, 2);
	}
	for (int i = 0; arMinko[i] != nil; i++)
	{
		if (ASEARCH(arMinko[i], arHaiWithoutFuro) >= 0)
			arRet += arMinko[i];
	}
	return arRet;
}

array getKakanHaiUseful(string hai, int nPlayer)
{
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	array arKakanHai = getKakanHai(hai);
	int shantenBefore = getShantenYaku(hai, bafuHai, jifuHai);
	array arKakanHaiUseful;
	for (int i = 0; arKakanHai[i] != nil; i++)
	{
		string h = arKakanHai[i];
		string strTehai = _strreplace(removeHai(hai, h), h + h + h, h + h + h + h);
		int shantenAfter = getShantenYaku(strTehai, bafuHai, jifuHai);
		if (shantenBefore >= shantenAfter)
			arKakanHaiUseful += h;
	}
	return arKakanHaiUseful;
}

int canAnkan(int nPlayer, string tsumoHai)
{
	array arAnkanHai = getAnkanHai(addHai(arTehai[nPlayer], tsumoHai));
	if (_aryvn(arAnkanHai) > 0)
		return 1;
	return 0;
}

int shouldAnkan(int nPlayer, string tsumoHai)
{
	string tehai = addHai(arTehai[nPlayer], tsumoHai);
	array arAnkanHaiUseful = getAnkanHaiUseful(tehai, nPlayer);
	if (_aryvn(arAnkanHaiUseful) > 0)
		return 1;
	return 0;
}

array getAnkanHai(string hai)
{
	array arHai = strHaiToArrayWithoutFuro(hai);
	array arRet;
	for (int i = 0; arHai[i] != nil; i++)
	{
		if (ASEARCH(arHai[i], arRet) == -1)
		{
			if (_aryvn(ASEARCHEX(arHai[i], arHai)) >= 4)
				arRet += arHai[i];
		}
	}
	return arRet;
}

array getAnkanHaiUseful(string hai, int nPlayer)
{
	string bafuHai = getBafuHai();
	string jifuHai = getJifuHai(nPlayer);
	array arAnkanHai = getAnkanHai(hai);
	int shantenBefore = getShantenYaku(hai, bafuHai, jifuHai);
	array arAnkanHaiUseful;
	for (int i = 0; arAnkanHai[i] != nil; i++)
	{
		string h = arAnkanHai[i];
		string strTehai = removeHai(hai, h + h + h + h) + "(" + h + h + h + h + ")";
		int shantenAfter = getShantenYaku(strTehai, bafuHai, jifuHai);
		if (shantenBefore >= shantenAfter)
			arAnkanHaiUseful += h;
	}
	return arAnkanHaiUseful;
}

string OnNotifyPrepareSuteComplete(dict ref)
{
	int selectPlayer = ref["Reference0"];
	string sute = ref["Reference1"];
	reservedScript = "\C\0\s[0]\b[2]\![raise,OnSuteComplete," + selectPlayer + "," + sute + "]";
	_create_thread("TH_WaitResponse");
	return "";
}

string OnSuteComplete(dict ref)
{
	int completePlayer = ref["Reference0"];
	string sute = ref["Reference1"];
	string s = "\![set,balloontimeout,0]\0\s[0]\b[2]\_q";
	dict reservedNakuCopy = reservedNaku;
	reservedNaku = ${
		$(playerName[0], {}),
		$(playerName[1], {}),
		$(playerName[2], {})
	};
	//AIがロンすることを選択した場合(ダブロンどうしよう)
	for (int i = 0; i < 3; i++)
	{
		string action = reservedNakuCopy[playerName[i]][0];
		if (action != "")
		{
			if (action == "ron")
			{
				s += showStatus("", sute, completePlayer, i, {0, 0, 0});
				//発声通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",say," + playerName[i] + ",ron]";
				s += "\x\![raise,OnChoiceSelectEx,,Menu_Ron," + i + "," + completePlayer + "," + sute + "]\e";
				return s;
			}
			if (action == "pon")
			{
				s += showStatus("", "", i, -1, {0, 0, 0}, 1);
				//発声通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",say," + playerName[i] + ",pon]";
				s += "\_w[2000]";
				//晒した牌通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",open," + playerName[completePlayer] + "," + sute + sute + sute + "]";
				//捨て牌問い合わせ
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",sutehai?]";
				setFuro(i, completePlayer, sute, sute + sute);
				setKyotaku(completePlayer);
				reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + ",]";
				_create_thread("TH_WaitResponse");
				return s;
			}
			if (action == "chi")
			{
				s += showStatus("", "", i, -1, {0, 0, 0}, 1);
				string hai1 = reservedNakuCopy[playerName[i]][1];
				string hai2 = reservedNakuCopy[playerName[i]][2];
				string furoHai = _arystr(sortHai(strHaiToArray(sute + hai1 + hai2)));
				//発声通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",say," + playerName[i] + ",chi]";
				s += "\_w[2000]";
				//晒した牌通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",open," + playerName[completePlayer] + "," + furoHai + "]";
				//捨て牌問い合わせ
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",sutehai?]";
				setFuro(i, completePlayer, sute, hai1 + hai2);
				setKyotaku(completePlayer);
				reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + ",]";
				_create_thread("TH_WaitResponse");
				return s;
			}
			if (action == "kan")
			{
				isRinshanChance = 1;
				setFuro(i, completePlayer, sute, sute + sute + sute);
				setKyotaku(completePlayer);
				string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
				dorahyouji += strDorahyoujiNew;
				string tsumo = arYama[nYamaIndex++];
				s += showStatus(tsumo, "", i, -1, {0, 0, 0}, 1);
				//発声通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",say," + playerName[i] + ",kan]";
				s += "\_w[2000]";
				//晒した牌通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",open," + playerName[completePlayer] + "," + sute + sute + sute + sute + "]";
				//ツモ通知
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",tsumo," + playerName[i] + "," + getLeftYama() + "," + tsumo + "]";
				//捨て牌問い合わせ
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",sutehai?]";
				//dora通知
				s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
					+ ",dora," + strDorahyoujiNew + "]";
				s += "\_q\e";
				reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + "," + tsumo + "]";
				_create_thread("TH_WaitResponse");
				return s;
			}
		}
		else//お任せモードの場合ロン可能ならロンする
		{
			if (i != completePlayer)
			{
				if (canRon(i, sute))
				{
					s += showStatus("", sute, completePlayer, i, {0, 0, 0});
					//発声通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",say," + playerName[i] + ",ron]";
					s += "\x\![raise,OnChoiceSelectEx,,Menu_Ron," + i + "," + completePlayer + "," + sute + "]\e";
					return s;
				}
				if (shouldPon(i, sute))
				{
					s += showStatus("", "", i, -1, {0, 0, 0}, 1);
					//発声通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",say," + playerName[i] + ",pon]";
					s += "\_w[2000]";
					//晒した牌通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",open," + playerName[completePlayer] + "," + sute + sute + sute + "]";
					//捨て牌問い合わせ
					s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
						+ ",sutehai?]";
					setFuro(i, completePlayer, sute, sute + sute);
					setKyotaku(completePlayer);
					reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + ",]";
					_create_thread("TH_WaitResponse");
					return s;
				}
				if ((completePlayer + 1) % 4 == i && shouldChi(i, sute))
				{
					s += showStatus("", "", i, -1, {0, 0, 0}, 1);
					string bafuHai = getBafuHai();
					string jifuHai = getJifuHai(i);
					array arMaterial = getChiMaterial(arTehai[i], sute);
					int shantenMin = 99;
					array indexMin = {};
					for (int j = 0; arMaterial[j] != nil; j++)
					{
						string haiChanged = removeHai(arTehai[i], arMaterial[j]) + "<" + _arystr(sortHai(strHaiToArray(arMaterial[j] + sute))) + ">";
						int shantenTmp = getShantenYaku(haiChanged, bafuHai, jifuHai);
						if (shantenTmp < shantenMin)
						{
							shantenMin = shantenTmp;
							indexMin = {j};
						}
						else if (shantenTmp == shantenMin)
						{
							indexMin += j;
						}
					}
					int index = _randselect(indexMin);
					array arMaterialSelected = strHaiToArray(arMaterial[index]);
					string hai1 = arMaterialSelected[0];
					string hai2 = arMaterialSelected[1];
					string furoHai = _arystr(sortHai(strHaiToArray(sute + hai1 + hai2)));
					//発声通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",say," + playerName[i] + ",chi]";
					s += "\_w[2000]";
					//晒した牌通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",open," + playerName[completePlayer] + "," + furoHai + "]";
					//捨て牌問い合わせ
					s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
						+ ",sutehai?]";
					setFuro(i, completePlayer, sute, hai1 + hai2);
					setKyotaku(completePlayer);
					reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + ",]";
					_create_thread("TH_WaitResponse");
					return s;
				}
				if (shouldDaiminkan(i, sute))
				{
					isRinshanChance = 1;
					setFuro(i, completePlayer, sute, sute + sute + sute);
					setKyotaku(completePlayer);
					string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
					dorahyouji += strDorahyoujiNew;
					string tsumo = arYama[nYamaIndex++];
					s += showStatus(tsumo, "", i, -1, {0, 0, 0}, 1);
					//発声通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",say," + playerName[i] + ",kan]";
					s += "\_w[2000]";
					//晒した牌通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",open," + playerName[completePlayer] + "," + sute + sute + sute + sute + "]";
					//ツモ通知
					s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
						+ ",tsumo," + playerName[i] + "," + getLeftYama() + "," + tsumo + "]";
					//捨て牌問い合わせ
					s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
						+ ",sutehai?]";
					//dora通知
					s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
						+ ",dora," + strDorahyoujiNew + "]";
					reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + i + "," + tsumo + "]";
					_create_thread("TH_WaitResponse");
					return s;
				}
			}
		}
	}
	setKyotaku(completePlayer);
	//流局
	if (_aryvn(arYama) == nYamaIndex)
	{
		s += showStatus("", sute, completePlayer, -1, {0, 0, 0});
		s += "\x[noclear]";
		isRecievedResponse = 1;
		for (int i = 0; i < 3; i++)
		{
			int shanten = getShanten(arTehai[i]);
			if (shanten == 0)
			{
				reservedTenpai[playerName[i]] = "yes";//デフォルトでyes
				//テンパイ宣言問い合わせ
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",tenpai?]";
				dResponseNeed[playerName[i]] = 1;
				isRecievedResponse = 0;
			}
		}
		s += "\![notify,OnNotifyPrepareRyukyoku]";
		s += "\_q\e";
		return s;
	}
	int nextPlayer = (completePlayer + 1) % 4;
	string tsumo;
	//userがツモ
	if (nextPlayer == 3)
	{
		tsumo = arYama[nYamaIndex++];
		s += showStatus(tsumo, "", 3, -1, {0, 0, 0});
		s += "\_q\e";
		lastScript = s;
		return s;
	}
	//user以外がツモ
	tsumo = arYama[nYamaIndex++];
	s += showStatus(tsumo, "", nextPlayer, -1, {0, 0, 0});
	//ツモ通知
	s += "\![raiseother," + playerName[nextPlayer] + ",OnMahjong," + getVersion()
		+ ",tsumo," + playerName[nextPlayer] + "," + getLeftYama() + "," + tsumo + "]";
	//捨て牌問い合わせ
	s += "\![raiseother," + playerName[nextPlayer] + ",OnMahjong," + getVersion()
		+ ",sutehai?]";
	s += "\_q\e";
	reservedScript = "\C\0\b[2]\![raise,OnSuteSelect," + nextPlayer + "," + tsumo + "]";
	_create_thread("TH_WaitResponse");
	return s;
}

setKyotaku(int nPlayer)
{
	if (arRichiJunme[nPlayer] == _aryvn(arKawa[nPlayer]) - 1)
	{
		kyotaku++;
		arScore[nPlayer] -= 1000;
	}
}

string OnNotifyPrepareRyukyoku(dict ref)
{
	reservedScript = "\C\0\s[0]\b[2]\![raise,OnRyukyoku]";
	_create_thread("TH_WaitResponse");
	return "";
}

string OnRyukyoku(dict ref)
{
	string m;
	array arTenpaiPlayerFlag = {0, 0, 0};
	for (int i = 0; i < 3; i++)
	{
		string action = reservedTenpai[playerName[i]];
		if (action == "yes")
		{
			arTenpaiPlayerFlag[i] = 1;
			m += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[i] + ",tenpai]";
			m += "\_w[1000]";
		}
		else
		{
			arTenpaiPlayerFlag[i] = 0;
			m += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
				+ ",say," + playerName[i] + ",noten]";
			m += "\_w[1000]";
		}
	}
	int shanten = getShanten(arTehai[3]);
	if (shanten == 0)
		arTenpaiPlayerFlag += 1;
	else
		arTenpaiPlayerFlag += 0;
	string s = "\![set,balloontimeout,0]\0\s[0]\b[2]\_q";
	s += showStatus("", "", -1, -1, arTenpaiPlayerFlag);
	s += m;
	s += "\x\![raise,OnRyukyokuScore," + _arystr(arTenpaiPlayerFlag) + "]\e";
	return s;
}

setSutehai(string sute, int nPlayer)
{
	arKawa[nPlayer] += sute;
	for (int i = 0; i < 4; i++)
	{
		if (arRichiJunme[i] >= 0)
			arFuritenCheckRichi[i] += sute;
		if (nPlayer == i)
			arFuritenCheckTurn[i] = {};
		else
			arFuritenCheckTurn[i] += sute;
	}
}

string Menu_Sutehai(dict ref)
{
	int selectPlayer = 3;
	string sute = ref["Reference2"];
	string tsumo = ref["Reference3"];
	isRinshanChance = 0;
	string s = "\![set,balloontimeout,0]\0\s[0]\b[2]\_q";
	//userが捨て
	setSutehai(sute, selectPlayer);
	arTehai[selectPlayer] = removeHai(addHai(arTehai[selectPlayer], tsumo), sute);
	s += showStatus("", sute, selectPlayer, -1, {0, 0, 0});
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",sutehai," + playerName[selectPlayer] + "," + sute + "]";
	isRecievedResponse = 1;
	for (int i = 0; playerName[i] != nil; i++)
	{
		if (i != selectPlayer)
		{
			array command = {};
			if (canRon(i, sute))
				command += "ron";
			if (canPon(i, sute))
				command += "pon";
			if (i == 0 && canChi(i, sute))
				command += "chi";
			if (_aryvn(command) > 0)
			{
				s += "\![raiseother," + playerName[i] + ",OnMahjong," + getVersion()
					+ ",naku?," + sute + "," + JOIN(command, ",") + "]";
				dResponseNeed[playerName[i]] = 1;
				isRecievedResponse = 0;
			}
		}
	}
	s += "\_q\e";
	reservedScript = "\C\0\s[0]\b[2]\![raise,OnSuteComplete," + selectPlayer + "," + sute + "]";
	_create_thread("TH_WaitResponse");
	return s;
}

string showStatus(string tsumo, string sute, int nPlayer, int agariPlayer, array arTenpaiPlayerFlag, int isFuro)
{
	string s;
	array wanpai;
	array arDorahyouji = strHaiToArray(dorahyouji);
	int isMinkan = 0;
	if (isFuro && tsumo != "")
		isMinkan = 1;
	for (int i = 0; i < 5; i++)
	{
		if (arDorahyouji[4 - i] == nil)
		{
			wanpai += "tb";
		}
		else
		{
			if (isMinkan)
			{
				wanpai += "tb";
				isMinkan = 0;
			}
			else
			{
				wanpai += arDorahyouji[4 - i];
			}
		}
	}
	wanpai += "tb";
	wanpai += "tb";
	array seki = {"東", "南", "西", "北"};
	dict dSeki;
	array pNames;
	for (int i = 0; playerName[i] != nil; i++)
	{
		pNames += playerName[(i + oyaIndex) % 4];
	}
	for (int i = 0; pNames[i] != nil; i++)
	{
		dSeki += $(pNames[i], seki[i]);
	}
	//王牌
	s += "\f[height,+16]\_l[110,-15]";
	for (int i = 0; wanpai[i] != nil; i++)
	{
		s += convertUnicode(wanpai[i]);
	}
	s += "\f[height,default]";
	//場風、局数、積み棒、供託棒
	s += "\_l[,0] " + arBafu[bafu] + kyoku + "局";
	s += "\_l[260,0]●x" + tsumibou + "\n";
	s += "\_l[260,]\f[color,red]●\f[color,default]x" + kyotaku;
	//0-2
	for (int p = 0; p < 3; p++)
	{
		s += "\_l[0," + (80 * p) + "]\f[height,default]" + dSeki[playerName[p]] + "家<<" + playerName[p] + ">>\n";
		s += "\f[height,+16]";
		if (openTehai || (p == agariPlayer) || arTenpaiPlayerFlag[p])
		{
			string tsumoShowing = "";
			if (nPlayer == p && tsumo != "")
				tsumoShowing = tsumo;
			s += showHai(arTehai[p], tsumoShowing, p, 0, 1);
		}
		else
		{
			string tsumoShowing = "";
			if (nPlayer == p && tsumo != "")
				tsumoShowing = tsumo;
			s += showHai(arTehai[p], tsumoShowing, p, 0, 0);
		}
		s += "\n";
		for (int i = 0; arKawa[p][i] != nil; i++)
		{
			if (i == arRichiJunme[p])
				s += "\f[color,red]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			else if (ASEARCH(i, arFuroJunme[p]) >= 0)
				s += "\f[color,gray]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			else if (agariPlayer >= 0 && p != agariPlayer && nPlayer == p && i == _aryvn(arKawa[p]) - 1)
				s += "\f[color,purple]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			else
				s += convertUnicode(arKawa[p][i]);
		}
	}
	//3
	int pUser = 3;
	s += "\_l[0,240]\f[height,default]" + dSeki[playerName[pUser]] + "家<<" + playerName[pUser] + ">>\n";
	s += "\f[height,+16]";
	for (int i = 0; arKawa[pUser][i] != nil; i++)
	{
		if (i == arRichiJunme[pUser])
			s += "\f[color,red]" + convertUnicode(arKawa[pUser][i]) + "\f[color,default]";
		else if (ASEARCH(i, arFuroJunme[pUser]) >= 0)
			s += "\f[color,gray]" + convertUnicode(arKawa[pUser][i]) + "\f[color,default]";
		else if (agariPlayer >= 0 && pUser != agariPlayer && nPlayer == pUser && i == _aryvn(arKawa[pUser]) - 1)
			s += "\f[color,purple]" + convertUnicode(arKawa[pUser][i]) + "\f[color,default]";
		else
			s += convertUnicode(arKawa[pUser][i]);
	}
	s += "\n";
	string tsumoShowing = "";
	if (nPlayer == pUser)
		tsumoShowing = tsumo;
	s += showHai(arTehai[pUser], tsumoShowing, pUser, isFuro, 1);
	s += "\f[height,default]";
	int n = getLeftYama();
	if (n > 0 || tsumo != "")
		s += "\n\n\_l[225,]残り: " + n + "枚";
	else
		s += "\n\n\_l[225,]流局";
	s += "\_l[0,]";
	if (nPlayer == pUser && tsumo != "")
	{
		if (canTsumo(pUser, tsumo))
			s += "\![*]\__q[Menu_Tsumo,3]ツモ\__q";
		if (canRichi(pUser, tsumo))
			s += "\![*]\__q[Menu_Richi]リーチ\__q";
		if (canKakan(pUser, tsumo))
		{
			array arKakanHai = getKakanHai(addHai(arTehai[pUser], tsumo));
			if (_aryvn(arKakanHai) == 1)
			{
				s += "\![*]\__q[Menu_Kakan," + arKakanHai[0] + "]カン\__q";
			}
			else
			{
				s += "\![*]カン";
				for (int i = 0; arKakanHai[i] != nil; i++)
				{
					s += " \__q[Menu_Kakan," + arKakanHai[i] + "]\f[height,+16]" + convertUnicode(arKakanHai[i]) + "\f[height,default]\__q";
				}
			}
		}
		if (canAnkan(pUser, tsumo))
		{
			array arAnkanHai = getAnkanHai(addHai(arTehai[pUser], tsumo));
			if (_aryvn(arAnkanHai) == 1)
			{
				s += "\![*]\__q[Menu_Ankan," + arAnkanHai[0] + "]カン\__q";
			}
			else
			{
				s += "\![*]カン";
				for (int i = 0; arAnkanHai[i] != nil; i++)
				{
					s += " \__q[Menu_Ankan," + arAnkanHai[i] + "]\f[height,+16]" + convertUnicode(arAnkanHai[i]) + "\f[height,default]\__q";
				}
			}
		}
	}
	if (nPlayer != pUser && sute != "" && agariPlayer == -1)
	{
		if (canRon(pUser, sute))
			s += "\![*]\__q[Menu_Ron,3," + nPlayer + "," + sute + "]ロン\__q";
		if (canPon(pUser, sute))
			s += "\![*]\__q[Menu_Pon,3," + nPlayer + "," + sute + "]ポン\__q";
		if (nPlayer + 1 == pUser && canChi(pUser, sute))
		{
			array arMaterial = getChiMaterial(arTehai[pUser], sute);
			if (_aryvn(arMaterial) == 1)
			{
				s += "\![*]\__q[Menu_Chi,3," + nPlayer + "," + sute + "," + arMaterial[0] + "]チー\__q";
			}
			else
			{
				s += "\![*]チー";
				for (int i = 0; arMaterial[i] != nil; i++)
				{
					array arMaterialSelected = strHaiToArray(arMaterial[i]);
					string hai1 = arMaterialSelected[0];
					string hai2 = arMaterialSelected[1];
					s += " \__q[Menu_Chi,3," + nPlayer + "," + sute + "," + arMaterial[i] + "]\f[height,+16]" + convertUnicode(hai1) + convertUnicode(hai2) + "\f[height,default]\__q";
				}
			}
		}
		if (canDaiminkan(pUser, sute))
			s += "\![*]\__q[Menu_Daiminkan,3," + nPlayer + "," + sute + "]カン\__q";
	}
	s += "\![*]\__q[Menu_CANCEL]閉じる\__q";
	return s;
}

int isMenzenPlayer(int nPlayer)
{
	string tehai = arTehai[nPlayer];
	for (int i = 0; i < _strlen(tehai); i++)
	{
		if (_substr(tehai, i, 1) == "<")
		{
			return 0;
		}
		else
		{
			i++;
		}
	}
	return 1;
}

string Menu_Richi(dict ref)
{	
	arRichiJunme[3] = _aryvn(arKawa[3]);
	string tsumo = arYama[nYamaIndex - 1];
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say,user,richi]";
	s += "\0\s[0]\b[2]\_q" + showStatus(tsumo, "", 3, -1, {0, 0, 0}, 0) + "\_q\e";
	return s;
}

string Menu_Kakan(dict ref)
{
	string kakanHai = ref["Reference2"];
	string tsumo = arYama[nYamaIndex - 1];
	int nFuroPlayer = 3;
	arTehai[nFuroPlayer] = addHai(arTehai[nFuroPlayer], tsumo);
	isRinshanChance = 1;
	setKakan(nFuroPlayer, kakanHai);
	dorahyouji += arYama[52 + (_strlen(dorahyouji) / 2)];
	tsumo = arYama[nYamaIndex++];
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say," + playerName[nFuroPlayer] + ",kan]";
	s += "\0\s[0]\b[2]\_q" + showStatus(tsumo, "", nFuroPlayer, -1, {0, 0, 0}, 1) + "\_q\e";
	lastScript = s;
	return s;
}

string Menu_Ankan(dict ref)
{
	string ankanHai = ref["Reference2"];
	string tsumo = arYama[nYamaIndex - 1];
	int nFuroPlayer = 3;
	arTehai[nFuroPlayer] = addHai(arTehai[nFuroPlayer], tsumo);
	isRinshanChance = 1;
	setAnkan(nFuroPlayer, ankanHai);
	dorahyouji += arYama[52 + (_strlen(dorahyouji) / 2)];
	tsumo = arYama[nYamaIndex++];
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say," + playerName[nFuroPlayer] + ",kan]";
	s += "\0\s[0]\b[2]\_q" + showStatus(tsumo, "", nFuroPlayer, -1, {0, 0, 0}, 0) + "\_q\e";
	lastScript = s;
	return s;
}

string Menu_Pon(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	setFuro(nFuroPlayer, nSutePlayer, sute, sute + sute);
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say," + playerName[nFuroPlayer] + ",pon]";
	s += "\0\s[0]\b[2]\_q" + showStatus("", "", nFuroPlayer, -1, {0, 0, 0}, 1) + "\_q\e";
	return s;
}

string Menu_Chi(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	string hai = ref["Reference5"];
	array arHai = strHaiToArray(hai);
	setFuro(nFuroPlayer, nSutePlayer, sute, arHai[0] + arHai[1]);
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say," + playerName[nFuroPlayer] + ",chi]";
	s += "\0\s[0]\b[2]\_q" + showStatus("", "", nFuroPlayer, -1, {0, 0, 0}, 1) + "\_q\e";
	return s;
}

string Menu_Daiminkan(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	isRinshanChance = 1;
	setFuro(nFuroPlayer, nSutePlayer, sute, sute + sute + sute);
	dorahyouji += arYama[52 + (_strlen(dorahyouji) / 2)];
	string tsumo = arYama[nYamaIndex++];
	string s;
	s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion()
		+ ",say," + playerName[nFuroPlayer] + ",kan]";
	s += "\0\s[0]\b[2]\_q" + showStatus(tsumo, "", nFuroPlayer, -1, {0, 0, 0}, 1) + "\_q\e";
	lastScript = s;
	return s;
}

setFuro(int nFuroPlayer, int nSutePlayer, string sute, string haiUsed)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], haiUsed);
	arTehai[nFuroPlayer] = addFuro(arTehai[nFuroPlayer], sute + haiUsed, "<", ">");
	arFuroJunme[nSutePlayer] += _aryvn(arKawa[nSutePlayer]) - 1;
	arFuroHistory[nFuroPlayer] += {sute, nSutePlayer};
}

setKakan(int nFuroPlayer, string kakanHai)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], kakanHai);
	arTehai[nFuroPlayer] = _strreplace(arTehai[nFuroPlayer], kakanHai + kakanHai + kakanHai, kakanHai + kakanHai + kakanHai + kakanHai);
}

setAnkan(int nFuroPlayer, string ankanHai)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], ankanHai + ankanHai + ankanHai + ankanHai);
	arTehai[nFuroPlayer] = addFuro(arTehai[nFuroPlayer], ankanHai + ankanHai + ankanHai + ankanHai, "(", ")");;
}

string addFuro(string tehai, string furo, string s1, string s2)
{
	string s;
	string sFuro;
	for (int i = 0; i < _strlen(tehai); i++)
	{
		if (_substr(tehai, i, 1) == "<")
		{
			if (_substr(tehai, i + 7, 1) == ">")
			{
				sFuro += _substr(tehai, i, 8);
				i += 7;
			}
			else if (_substr(tehai, i + 9, 1) == ">")
			{
				sFuro += _substr(tehai, i, 10);
				i += 9;
			}
		}
		else if (_substr(tehai, i, 1) == "(")
		{
			if (_substr(tehai, i + 9, 1) == ")")
			{
				sFuro += _substr(tehai, i, 10);
				i += 9;
			}
		}
		else
		{
			s += _substr(tehai, i, 2);
			i++;
		}
	}
	string strFuro = _arystr(sortHai(strHaiToArray(furo)));
	s = s + s1 + strFuro + s2 + sFuro;
	return s;
}

string Menu_CANCEL(dict ref)
{
	return "\e";
}

clearStatus()
{
	arYama = {};
	arTehai = {"", "", "", ""};
	arKawa = {{}, {}, {}, {}};
	arFuritenCheckRichi = {{}, {}, {}, {}};
	arFuritenCheckTurn = {{}, {}, {}, {}};
	arRichiJunme = {-1, -1, -1, -1};
	arFuroJunme = {{}, {}, {}, {}};
	arFuroHistory = {{}, {}, {}, {}};
	isRinshanChance = 0;
	dorahyouji = "";
	nYamaIndex = 0;
	lastScript = "";
}

//全牌
array getAllHai()
{
	array a = {
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z"
	};//n = 136
	return a;
}

string showHai(string strHai, string tsumoShowing, int nPlayer, int isFuro, int isOpen, int isScoreView)
{
	string s;
	array arHai = strHaiToArrayWithFuro(strHai);
	if (nPlayer != 3 || isScoreView)
	{
		string sFuro;
		int nFuro;
		int nFuroOffset;
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (arHai[i][0] == "normal")
			{
				if (isOpen)
					s += convertUnicode(arHai[i][1]);
				else
					s += convertUnicode("tb");
			}
			else if (arHai[i][0] == "furo")
			{
				sFuro += showFuroWithColor(arHai[i][1], nPlayer, nFuro);
				nFuro++;
				nFuroOffset += _strlen(arHai[i][1]) / 2;
			}
			else if (arHai[i][0] == "ankan")
			{
				sFuro += showAnkan(arHai[i][1]);
				nFuroOffset += _strlen(arHai[i][1]) / 2;
			}
		}
		if (tsumoShowing != "")
		{
			s += " ";
			if (isOpen)
				s += convertUnicode(tsumoShowing);
			else
				s += convertUnicode("tb");
		}
		if (sFuro != "")
			s += "\_l[" + (260 - (15 * nFuroOffset)) + ",]" + sFuro;
	}
	else
	{
		string sFuro;
		int nFuro;
		int nFuroOffset;
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (tsumoShowing != "")
			{
				if (arHai[i][0] == "normal")
				{
					if (arRichiJunme[3] == -1)
					{
						s += "\__q[Menu_Sutehai," + arHai[i][1] + "," + tsumoShowing + "]" + convertUnicode(arHai[i][1]) + "\__q";
					}
					else if (arRichiJunme[3] == _aryvn(arKawa[3]))
					{
						int shanten = getShanten(removeHai(addHai(arTehai[3], tsumoShowing), arHai[i][1]));
						if (shanten == 0)
							s += "\__q[Menu_Sutehai," + arHai[i][1] + "," + tsumoShowing + "]" + convertUnicode(arHai[i][1]) + "\__q";
						else
							s += "\f[color,gray]" + convertUnicode(arHai[i][1]) + "\f[color,default]";
					}
					else
					{
						s += convertUnicode(arHai[i][1]);
					}
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], nPlayer, nFuro);
					nFuro++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
			}
			else if (isFuro && tsumoShowing == "")
			{
				if (arHai[i][0] == "normal")
				{
					s += "\__q[Menu_Sutehai," + arHai[i][1] + ",]" + convertUnicode(arHai[i][1]) + "\__q";
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], nPlayer, nFuro);
					nFuro++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
			}
			else
			{
				if (arHai[i][0] == "normal")
				{
					s += convertUnicode(arHai[i][1]);
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], nPlayer, nFuro);
					nFuro++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					nFuroOffset += _strlen(arHai[i][1]) / 2;
				}
			}
		}
		if (tsumoShowing != "")
		{
			s += " ";
			if (arRichiJunme[3] == _aryvn(arKawa[3]))
			{
				int shanten = getShanten(arTehai[3]);
				if (shanten == 0)
					s += "\__q[Menu_Sutehai," + tsumoShowing + "," + tsumoShowing + "]" + convertUnicode(tsumoShowing) + "\__q";
				else
					s += "\f[color,gray]" + convertUnicode(tsumoShowing) + "\f[color,default]";
			}
			else
			{
				s += "\__q[Menu_Sutehai," + tsumoShowing + "," + tsumoShowing + "]" + convertUnicode(tsumoShowing) + "\__q";
			}
		}
		if (sFuro != "")
			s += "\_l[" + (260 - (15 * nFuroOffset)) + ",]" + sFuro;
	}
	return s;
}

string showFuroWithColor(string strFuro, int nPlayer, int nFuroIndex)
{
	nFuroIndex = (_aryvn(arFuroHistory[nPlayer]) - 1) - nFuroIndex;
	string strFuroHai = arFuroHistory[nPlayer][nFuroIndex][0];
	int nSutePlayer = arFuroHistory[nPlayer][nFuroIndex][1];
	strFuro = strFuroHai + removeHai(strFuro, strFuroHai);
	string s;
	if (_strlen(strFuro) == 6)
	{
		if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 0, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 0, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 2, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 2, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 4, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 4, 2));
	}
	else if (_strlen(strFuro) == 8)
	{
		if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 0, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 0, 2));
		s += convertUnicode(_substr(strFuro, 2, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 4, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 4, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 6, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 6, 2));
	}
	return s;
}

string showAnkan(string strAnkan)
{
	string hai = _substr(strAnkan, 0, 2);
	return convertUnicode("tb") + convertUnicode(hai) + convertUnicode(hai) + convertUnicode("tb");
}

string addHai(string strHai1, string strHai2)
{
	array arHai2 = strHaiToArray(strHai2);
	string strToAdd = strHai1;
	for (int i = 0; arHai2[i] != nil; i++)
	{
		string strAdded = "";
		int isAdded = 0;
		for (int j = 0; j < _strlen(strToAdd); j++)
		{
			if (_substr(strToAdd, j, 1) == "<")
			{
				if (!isAdded)
				{
					isAdded = 1;
					strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
				}
				if (_substr(strToAdd, j + 7, 1) == ">")
				{
					strAdded += _substr(strToAdd, j, 8);
					j += 7;
				}
				else if (_substr(strToAdd, j + 9, 1) == ">")
				{
					strAdded += _substr(strToAdd, j, 10);
					j += 9;
				}
			}
			else if (_substr(strToAdd, j, 1) == "(")
			{
				if (!isAdded)
				{
					isAdded = 1;
					strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
				}
				if (_substr(strToAdd, j + 9, 1) == ")")
				{
					strAdded += _substr(strToAdd, j, 10);
					j += 9;
				}
			}
			else
			{
				strAdded += _substr(strToAdd, j, 2);
				j++;
			}
		}
		if (!isAdded)
		{
			strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
		}
		strToAdd = strAdded;
	}
	return strToAdd;
}

string removeHai(string strHai1, string strHai2)
{
	array arHai2 = strHaiToArray(strHai2);
	string strToRemove = strHai1;
	for (int i = 0; arHai2[i] != nil; i++)
	{
		string strRemoved = "";
		int isRemoved = 0;
		for (int j = 0; j < _strlen(strToRemove); j++)
		{
			if (_substr(strToRemove, j, 1) == "<")
			{
				if (_substr(strToRemove, j + 7, 1) == ">")
				{
					strRemoved += _substr(strToRemove, j, 8);
					j += 7;
				}
				else if (_substr(strToRemove, j + 9, 1) == ">")
				{
					strRemoved += _substr(strToRemove, j, 10);
					j += 9;
				}
			}
			else if (_substr(strToRemove, j, 1) == "(")
			{
				if (_substr(strToRemove, j + 9, 1) == ")")
				{
					strRemoved += _substr(strToRemove, j, 10);
					j += 9;
				}
			}
			else
			{
				if (!isRemoved && _substr(strToRemove, j, 2) == arHai2[i])
				{
					isRemoved = 1;
				}
				else
				{
					strRemoved += _substr(strToRemove, j, 2);
				}
				j++;
			}
		}
		strToRemove = strRemoved;
	}
	return strToRemove;
}

array removeElementByName(array ary, string name, int count)
{
	array arRet;
	int n = 0;
	for (int i = 0; ary[i] != nil; i++)
	{
		if (ary[i] == name && n < count)
		{
			n++;
		}
		else
		{
			arRet += ary[i];
		}
	}
	return arRet;
}

array strHaiToArray(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		arRet += _substr(strHai, i, 2);
		i++;
	}
	return arRet;
}

array strHaiToArrayWithoutFuro(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		if (_substr(strHai, i, 1) == "<" || _substr(strHai, i, 1) == "(")
			return arRet;
		arRet += _substr(strHai, i, 2);
		i++;
	}
	return arRet;
}

array strHaiToArrayWithFuro(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		if (_substr(strHai, i, 1) == "<")
		{
			if (_substr(strHai, i + 7, 1) == ">")
			{
				arRet += {"furo", _substr(strHai, i + 1, 6)};
				i += 7;
			}
			else if (_substr(strHai, i + 9, 1) == ">")
			{
				arRet += {"furo", _substr(strHai, i + 1, 8)};
				i += 9;
			}
		}
		else if (_substr(strHai, i, 1) == "(")
		{
			if (_substr(strHai, i + 9, 1) == ")")
			{
				arRet += {"ankan", _substr(strHai, i + 1, 8)};
				i += 9;
			}
		}
		else
		{
			arRet += {"normal", _substr(strHai, i, 2)};
			i++;
		}
	}
	return arRet;
}

array shuffleArray(array a)
{
	array arRet;
	while (_aryvn(a) > 0)
	{
		int n = _aryvn(a);
		int index = _rand() * n / 10000;
		array an = {};
		for (int i = 0; i < n; i++)
		{
			if (i == index)
				arRet += a[i];
			else
				an += a[i];
		}
		a = an;
	}
	return arRet;
}

array getArrayRange(array a, int start, int end)
{
	array arRet;
	int x;
	int y;
	if (start <= end)
	{
		x = start;
		y = end;
	}
	else
	{
		x = end;
		y = start;
	}
	for (int i = x; i <= y; i++)
	{
		arRet += a[i];
	}
	return arRet;
}

string JOIN(array a, string delim)
{
	if (_aryvn(a) < 1)
		return "";
	if (_aryvn(a) < 2)
		return a[0];
	string s = a[0];
	for (int i = 1; a[i] != nil; i++)
	{
		s += delim + a[i];
	}
	return s;
}

//ソート
array sortHai(array a)
{
	array arRet;
	//バブルソートでいいや…
	while (_aryvn(a) > 0)
	{
		int n = _aryvn(a);
		int index = -1;
		int minValue = 99;
		array an = {};
		for (int i = 0; i < n; i++)
		{
			int v = getSortCode(a[i]);
			if (v < minValue) {
				if (index >= 0)
					an += a[index];
				index = i;
				minValue = v;
			}
			else
			{
				an += a[i];
			}
		}
		arRet += a[index];
		a = an;
	}
	return arRet;
}

//\_u[0xXXXXX]形式に変換
string convertUnicode(string hai)
{
	dict d = ${
		$("1m", "1F007"),//一萬
		$("2m", "1F008"),
		$("3m", "1F009"),
		$("4m", "1F00A"),
		$("5m", "1F00B"),
		$("6m", "1F00C"),
		$("7m", "1F00D"),
		$("8m", "1F00E"),
		$("9m", "1F00F"),
		$("1p", "1F019"),//一筒
		$("2p", "1F01A"),
		$("3p", "1F01B"),
		$("4p", "1F01C"),
		$("5p", "1F01D"),
		$("6p", "1F01E"),
		$("7p", "1F01F"),
		$("8p", "1F020"),
		$("9p", "1F021"),
		$("1s", "1F010"),//一索
		$("2s", "1F011"),
		$("3s", "1F012"),
		$("4s", "1F013"),
		$("5s", "1F014"),
		$("6s", "1F015"),
		$("7s", "1F016"),
		$("8s", "1F017"),
		$("9s", "1F018"),
		$("1z", "1F000"),//東
		$("2z", "1F001"),//南
		$("3z", "1F002"),//西
		$("4z", "1F003"),//北
		$("5z", "1F006"),//白
		$("6z", "1F005"),//発
		$("7z", "1F004"),//中
		$("tb", "1F02B")//Tile Back
	};
	return "\_u[0x" + d[hai] + "]";
}

//ソート順定義
int getSortCode(string hai)
{
	string s = "1m2m3m4m5m6m7m8m9m1p2p3p4p5p6p7p8p9p1s2s3s4s5s6s7s8s9s1z2z3z4z5z6z7z";
	int index = _strstr(s, hai);
	int r = index / 2;
	return r;
}
