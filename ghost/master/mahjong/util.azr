//******************************************************************************
// 設定
//******************************************************************************
//手牌オープン
int openTehai = 0;
//通信のタイムアウト設定(s)
double timeout = 2.000;
//通信にDirectSSTPを使用する
int useDSSTP = 1;

//******************************************************************************
// グローバル変数
//******************************************************************************
//ゲームで使用する変数
array arYama;
array arTehai = {"", "", "", ""};
array arKawa = {{}, {}, {}, {}};
array arFuritenCheckRichi = {{}, {}, {}, {}};
array arFuritenCheckTurn = {{}, {}, {}, {}};
array arScore = {25000, 25000, 25000, 25000};
array arRichiJunme = {-1, -1, -1, -1};
array arFuroJunme = {{}, {}, {}, {}};
array arFuroHistory = {{}, {}, {}, {}};
array arKakanHistory =  {{}, {}, {}, {}};
string dorahyouji;
string visiblePai;
int nYamaIndex;
int bafu = 0;//東:0, 南:1
array arBafu = {"東", "南"};
int kyoku = 0;//1 - 4
int oyaIndex = -1;
int tsumibou;
int kyotaku;
array playerName;
int isRinshanChance;
array arIppatsuChance = {0, 0, 0, 0};
array arChihouChance = {1, 1, 1, 1};
array arWRichi = {0, 0, 0, 0};
int isSoloMode;
int modeEx;
//バルーン表示保存用
string lastScript;
int isLockedSingleClick;
int isLockedDoubleClick;
//シェル表示保存用
array arSavedTehai = {{}, {}, {}, {}};
string savedTsumo;
array arSavedWordCommand;
string reservedClickScript;
//クライアントからのレスポンス保存用
string reservedSelectedHai;
int reservedRichi;
int reservedTsumo;
int reservedKakan;
int reservedAnkan;
dict reservedNaku;
dict reservedTenpai;
string reservedScript;
dict dResponseNeed;
int isRecievedResponse;

//******************************************************************************
// OnMahjongResponse
//******************************************************************************
string OnMahjongResponse(dict ref)
{
	string version = ref["Reference0"];
	if (version != getVersion())
		return "";
	string command = ref["Reference1"];
	string name = ref["Sender"];
	if (command != "hello")
	{
		if (dResponseNeed[name] != command)
		{
			senderrorlog("warning", name + "'s '" + command + "' command failed, maybe timeout.");
			return "";
		}
		dResponseNeed[name] = "";
	}
	switch (command)
	{
	case "hello":
		array a = _strsplit(ref["Reference3"], "=");
		if (a[0] == "name")
			name = a[1];
		else
			return "";
		if (_aryvn(playerName) >= 3)
			return "";//定員オーバー
		if (ASEARCH(name, playerName) >= 0)
			return "";//同名のキャラが参加済
		playerName += name;
		if (_aryvn(playerName) == 3)
		{
			playerName += "user";
			reservedScript = "\![raise,OnMahjongGameStart]";
			isRecievedResponse = 1;
			_create_thread("TH_WaitResponse");
			return "";
		}
		else
		{
			return "";
		}
		break;
	case "sutehai?":
		string action = ref["Reference2"];
		switch (action)
		{
		case ""://お任せモード
			break;
		case "sutehai":
			reservedSelectedHai = ref["Reference3"];
			break;
		case "richi":
			reservedSelectedHai = ref["Reference3"];
			reservedRichi = 1;
			break;
		case "tsumo":
			reservedTsumo = 1;
			break;
		case "kakan":
			reservedSelectedHai = ref["Reference3"];
			reservedKakan = 1;
			break;
		case "ankan":
			reservedSelectedHai = ref["Reference3"];
			reservedAnkan = 1;
			break;
		default:
			break;
		}
		isRecievedResponse = 1;
		break;
	case "naku?":
		reservedNaku[name] = {ref["Reference2"], ref["Reference3"], ref["Reference4"]};
		int count = 0;
		for (int i = 0; i < 3; i++)
		{
			if (dResponseNeed[playerName[i]] == command)
				count++;
		}
		if (count == 0)
			isRecievedResponse = 1;
		break;
	case "tenpai?":
		string action = ref["Reference2"];
		reservedTenpai[name] = action;
		int count = 0;
		for (int i = 0; i < 3; i++)
		{
			if (dResponseNeed[playerName[i]] == command)
				count++;
		}
		if (count == 0)
			isRecievedResponse = 1;
		break;
	default:
		break;
	}
	return "";
}

TH_WaitResponse()
{
	double wait = 0.125;
	int nLoop = 8 * timeout;
	for (int i = 0; i < nLoop; i++)
	{
		if (isSoloMode)
			break;
		_sleep(wait);
		if (isRecievedResponse)
		{
			isRecievedResponse = 0;
			break;
		}
	}
	for (int i = 0; i < 3; i++)
	{
		if (isSoloMode)
			break;
		if (dResponseNeed[playerName[i]] != "" && dResponseNeed[playerName[i]] != nil)
		{
			_speak("\![raise,OnMahjongResponseTimeout," + playerName[i] + "," + dResponseNeed[playerName[i]] + "]");
			dResponseNeed[playerName[i]] = "";
			_sleep(wait);
		}
	}
	string s = reservedScript;
	reservedScript = "";
	if (s != "")
		_speak(s);
}

string OnMahjongResponseTimeout(dict ref)
{
	string name = ref["Reference0"];
	string command = ref["Reference1"];
	senderrorlog("info", name + "'s '" + command + "' command timeout.");
	return "";
}

string getVersion()
{
	return "UKAJONG/0.2";
}

//******************************************************************************
// 表示
//******************************************************************************
string showBalloon()
{
	if (isLockedDoubleClick)
		return "";
	isLockedSingleClick = 0;//緊急避難的解除
	string s = "\0\_q";
	string shellName = _systemdict["OnNotifySelfInfo"]["Reference3"];
	string balloonName = _systemdict["OnNotifySelfInfo"]["Reference5"];
	if (shellName != "master" && balloonName == "雀卓用透明バルーン")
	{
		modeEx = 1;
		s += "\![lock,balloonmove]";
	}
	else
	{
		modeEx = 0;
		s += "\![unlock,balloonmove]";
	}
	//接続確認
	if (_aryvn(playerName) < 3)
	{
		if (modeEx)
		{
			s += "\_l[300,300]\![*]\__q[Menu_SearchPlayer]対局相手を探す\__q\n";
			s += "\_l[300,]\![*]\__q[Menu_StartWithoutPlayer]対局相手無しで始める\__q\n\n";
			s += "\_l[300,]\![*]\__q[Menu_CANSEL]閉じる\__q\_q\e";
		}
		else
		{
			s += "\![*]\__q[Menu_SearchPlayer]対局相手を探す\__q\n";
			s += "\![*]\__q[Menu_StartWithoutPlayer]対局相手無しで始める\__q\n\n";
			s += "\![*]\__q[Menu_CANSEL]閉じる\__q\_q\e";
		}
		return s;
	}
	if (nYamaIndex > 0)
		return lastScript;
	else
		return startKyoku();
}

//gamestart通知
string OnMahjongGameStart(dict ref)
{
	bafu = 0;
	kyoku = 1;
	oyaIndex = _randselect({0, 1, 2, 3});
	array seki = {"東", "南", "西", "北"};
	dict dSeki;
	array pNames;
	for (int i = 0; playerName[i] != nil; i++)
	{
		pNames += playerName[(i + oyaIndex) % 4];
	}
	for (int i = 0; pNames[i] != nil; i++)
	{
		dSeki += $(pNames[i], seki[i]);
	}
	string s;
	for (int i = 0; i < 3; i++)
	{
		s += notifyByIndex(i, {"gamestart", dSeki[playerName[i]], pNames[0], pNames[1], pNames[2], pNames[3]});
	}
	//Wallet of Unyu から出金
	string wou_id = "c58b6320-caf8-11ed-a901-0800200c9a66";
	if (isInstalledPlugin(wou_id)) {
		s += "\![raiseplugin," + wou_id + ",OnWalletOfUnyu,set," + (-1 * arScore[3]) + ",賭金徴収]";
	}
	return s + "\_w[2000]\![raise,startKyoku]\e";
}

int isInstalledPlugin(string search_id)
{
	for (int i = 0; _systemdict["installedplugin"]["Reference" + i] != nil; i++) {
		string plugin_id = _strsplit(_systemdict["installedplugin"]["Reference" + i], _bytechar(1))[1];
		if (search_id == plugin_id) {
			return 1;
		}
	}
	return 0;
}

string Menu_SearchPlayer(dict ref)
{
	isSoloMode = 0;
	isRecievedResponse = 0;
	if (useDSSTP)
	{
		array arFmo = _getfmo();
		array names;
		string txt = ".name" + _bytechar(1);
		for (int i = 0; arFmo[i] != nil; i++)
		{
			if (_strstr(arFmo[i], txt) >= 0)
			{
				string name = _strsplit(arFmo[i], txt)[1];
				if (name != _systemdict["OnNotifySelfInfo"]["Reference1"])
					names += name;
			}
		}
		for (int i = 0; names[i] != nil; i++)
		{
			dict d = notifyBySSTP(_getghosthwnd(names[i]), {"hello"});
			redirectSSTPtoNotify(d, names[i]);
		}
		return "\e";//何か返さないとOnChoiceSelectEx -> OnChoiceSelect、と2回呼ばれてしまう
	}
	else
	{
		return "\![raiseother,__SYSTEM_ALL_GHOST__,OnMahjong," + getVersion() + ",hello]\e";
	}
}

redirectSSTPtoNotify(dict d, string sender)
{
	if ((int)d["Result"] >= 3)
	{
		dict ref;
		ref += $("Sender", sender);
		for (int j = 0; d["Value" + j] != nil; j++)
		{
			if (_strstr(d["Value" + j], "X-SSTP-PassThru-") >= 0)
			{
				string header = _strsplit(d["Value" + j], "X-SSTP-PassThru-")[1];
				array arHeader = _strsplit(header, ": ");
				string key = arHeader[0];
				string value = arHeader[1];
				ref += $(key, value);
			}
		}
		if (_dicvn(ref) > 1)
		{
			OnMahjongResponse(ref);
		}
	}
}

string Menu_StartWithoutPlayer(dict ref)
{
	isSoloMode = 1;
	playerName = {"エミリ", "テディ", "エミリオ", "user"};
	bafu = 0;
	kyoku = 1;
	oyaIndex = _randselect({0, 1, 2, 3});
	//Wallet of Unyu に送金
	string s;
	string wou_id = "c58b6320-caf8-11ed-a901-0800200c9a66";
	if (isInstalledPlugin(wou_id)) {
		s += "\![raiseplugin," + wou_id + ",OnWalletOfUnyu,set," + (-1 * arScore[3]) + ",賭金徴収]";
	}
	return s + startKyoku();
}

string Menu_StartKyoku(dict ref)
{
	if (bafu < 2)//東場、南場
		return startKyoku();
	else
		return Menu_EndGame(ref);
}

string Menu_EndGame(dict ref)
{
	string s = "\![set,balloontimeout,0]\0\s[0]\_q";
	//gameend通知
	array arScoreNotify = {"gameend"};
	for (int i = 0; playerName[i] != nil; i++)
	{
		arScoreNotify += playerName[i] + _bytechar(1) + arScore[i];
	}
	s += notifyAll(arScoreNotify);
	//点数表示
	array sortedIndex;
	while (_aryvn(sortedIndex) < _aryvn(arScore))
	{
		int maxScore = -1000000;
		int maxIndex = -1;
		for (int i = 0; arScore[i] != nil; i++)
		{
			if (ASEARCH(i, sortedIndex) == -1)
			{
				if (arScore[i] > maxScore)
				{
					maxScore = arScore[i];
					maxIndex = i;
				}
			}
		}
		sortedIndex += maxIndex;
	}
	int x = 0;
	if (modeEx)
	{
		s += "\_l[,300]";
		x = 300;
	}
	for (int i = 0; sortedIndex[i] != nil; i++)
	{
		s += "\_l[" + x + ",]" + (i + 1) + ": " + playerName[sortedIndex[i]] + "\_l[" + (x + 150) + ",]" + arScore[sortedIndex[i]] + "\n";
	}
	//Wallet of Unyu に送金
	string wou_id = "c58b6320-caf8-11ed-a901-0800200c9a66";
	if (isInstalledPlugin(wou_id)) {
		s += "\![raiseplugin," + wou_id + ",OnWalletOfUnyu,set," + arScore[3] + ",賭金払い戻し]";
	}
	s += "\_q\e";
	playerName = {};
	arScore = {25000, 25000, 25000, 25000};
	tsumibou = 0;
	kyotaku = 0;
	isLockedSingleClick = 0;
	isLockedDoubleClick = 0;
	clearStatus();
	return s;
}

string startKyoku()
{
	arYama = shuffleArray(getAllHai());
	arTehai[0] = _arystr(sortHai(getArrayRange(arYama, 0, 12)));
	arTehai[1] = _arystr(sortHai(getArrayRange(arYama, 13, 25)));
	arTehai[2] = _arystr(sortHai(getArrayRange(arYama, 26, 38)));
	arTehai[3] = _arystr(sortHai(getArrayRange(arYama, 39, 51)));
	arKawa = {{}, {}, {}, {}};
	arFuritenCheckRichi = {{}, {}, {}, {}};
	arFuritenCheckTurn = {{}, {}, {}, {}};
	arRichiJunme = {-1, -1, -1, -1};
	arFuroJunme = {{}, {}, {}, {}};
	arFuroHistory = {{}, {}, {}, {}};
	arKakanHistory = {{}, {}, {}, {}};
	isRinshanChance = 0;
	arIppatsuChance = {0, 0, 0, 0};
	arChihouChance = {1, 1, 1, 1};
	arWRichi = {0, 0, 0, 0};
	dorahyouji = arYama[52];
	visiblePai = dorahyouji;
	nYamaIndex = 66;//王牌14枚(from 52 to 65)抜く
	reservedNaku = ${
		$(playerName[0], {}),
		$(playerName[1], {}),
		$(playerName[2], {}),
		$(playerName[3], {})
	};
	reservedTenpai = ${
		$(playerName[0], ""),
		$(playerName[1], ""),
		$(playerName[2], "")
	};
	dResponseNeed = ${
		$(playerName[0], ""),
		$(playerName[1], ""),
		$(playerName[2], "")
	};
	string tsumo = arYama[nYamaIndex++];
	string s;
	//kyokustart通知
	s += notifyAll({"kyokustart", arBafu[bafu], playerName[oyaIndex], tsumibou, kyotaku});
	//point通知
	for (int i = 0; i < 4; i++)
	{
		s += notifyAll({"point", playerName[i], "=", arScore[i]});
	}
	//haipai通知
	for (int i = 0; i < 3; i++)
	{
		s += notifyByIndex(i, {"haipai", playerName[i], arTehai[i]});
	}
	//dora通知
	s += notifyAll({"dora", dorahyouji});
	//userがツモ
	if (oyaIndex == 3)
	{
		isLockedSingleClick = 0;
		isLockedDoubleClick = 0;
		s += "\![set,balloontimeout,0]\![set,choicetimeout,0]\0\b[2]\_q";
		s += showStatus(tsumo, "", oyaIndex, -1, {0, 0, 0}, "", 0, -1);
		s += "\_q\e";
		lastScript = s;
		return s;
	}
	//user以外がツモ
	isLockedSingleClick = 1;
	isLockedDoubleClick = 1;
	s += "\![set,balloontimeout,0]\0\b[2]\_q";
	s += showStatus(tsumo, "", oyaIndex, -1, {0, 0, 0}, "", 0, -1);
	s += "\_q";
	s += notifyByIndex(oyaIndex, {"tsumo", playerName[oyaIndex], getLeftYama(), tsumo});
	s += notifyByIndex(oyaIndex, {"sutehai?"});
	s += "\![notify,OnNotifyPrepareSuteSelect," + oyaIndex + "," + tsumo + "]";
	s += "\e";
	return s;
}

int getLeftYama()
{
	return _aryvn(arYama) - nYamaIndex;
}

//東家を先頭にしてリーチ済の他家の現物を返す
array getGenbutsu(int p)
{
	array r;
	int findOya = 0;
	int i = 0;
	while (_aryvn(r) < 4)
	{
		if (i == oyaIndex)
			findOya = 1;
		if (findOya)
		{
			if (i != p && _aryvn(arFuritenCheckRichi[i]) > 0)
				r += _arystr(sortHai(uniq(strHaiToArray(_arystr(arKawa[i]) + _arystr(arFuritenCheckRichi[i])))));
			else
				r += "";
		}
		i = (i + 1) % 4;
	}
	return r;
}

//現在見えている牌
string getVisiblePai(int p)
{
	return visiblePai + removeFuro(arTehai[p]);
}

string OnSuteSelect(dict ref)
{
	int selectPlayer = ref["Reference0"];
	string tsumo = ref["Reference1"];
	string s = "\C\![lock,balloonrepaint]\![lock,repaint]\c\![set,balloontimeout,0]\![set,choicetimeout,0]\0\b[2]\_q";
	string sute;
	string tehai = addHai(arTehai[selectPlayer], tsumo);
	string wait = "";
	//要求されたアクションが可能かチェック
	string action;
	string selectedHai;
	string errorMessage;
	if (reservedTsumo)
	{
		if (canTsumo(selectPlayer, tsumo))
			action = "tsumo";
		else
			errorMessage = "failed requested action 'tsumo' with " + arTehai[selectPlayer] + " " + tsumo + " from " + playerName[selectPlayer];
	}
	else if (reservedRichi)
	{
		if (canRichi(selectPlayer, tsumo))
			action = "richi";
		else
			errorMessage = "failed requested action 'richi' with " + reservedSelectedHai + " from " + playerName[selectPlayer] + " [" + tehai + "]";
	}
	else if (reservedAnkan)
	{
		if (canAnkan(selectPlayer, tsumo) && (arRichiJunme[selectPlayer] == -1 || arRichiJunme[selectPlayer] >= 0 && tsumo == reservedSelectedHai))
			action = "ankan";
		else
			errorMessage = "failed requested action 'ankan' with " + reservedSelectedHai + " from " + playerName[selectPlayer] + " [" + tehai + "]";
	}
	else if (reservedKakan)
	{
		if (canKakan(selectPlayer, tsumo))
			action = "kakan";
		else
			errorMessage = "failed requested action 'kakan' with " + reservedSelectedHai + " from " + playerName[selectPlayer] + " [" + tehai + "]";
	}
	if (reservedSelectedHai != "")
		selectedHai = reservedSelectedHai;
	reservedTsumo = 0;
	reservedRichi = 0;
	reservedAnkan = 0;
	reservedKakan = 0;
	reservedSelectedHai = "";
	if (errorMessage != "")
	{
		//指定されたアクションが不可能だった
		senderrorlog("warning", errorMessage);
	}
	if (action == "" && selectedHai == "" && selectPlayer != 3)//auto
	{
		if (tsumo == "")//カン以外の副露直後
		{
			selectedHai = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji, getGenbutsu(selectPlayer), getVisiblePai(selectPlayer));
		}
		else
		{
			if (canTsumo(selectPlayer, tsumo))
			{
				action = "tsumo";
			}
			else if (shouldAnkan(selectPlayer, tsumo))
			{
				action = "ankan";
				array arAnkanHaiUseful = getAnkanHaiUseful(tehai, selectPlayer);
				string ankanHai = _randselect(arAnkanHaiUseful);
				if (arRichiJunme[selectPlayer] >= 0)
					ankanHai = tsumo;
				selectedHai = ankanHai;
			}
			else if (shouldKakan(selectPlayer, tsumo))
			{
				action = "kakan";
				array arKakanHaiUseful = getKakanHaiUseful(tehai, selectPlayer);
				string kakanHai = _randselect(arKakanHaiUseful);
				selectedHai = kakanHai;
			}
			else if (shouldRichi(selectPlayer, tsumo))
			{
				action = "richi";
				selectedHai = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji, getGenbutsu(selectPlayer), getVisiblePai(selectPlayer));
			}
			else
			{
				if (arRichiJunme[selectPlayer] >= 0)
					selectedHai = tsumo;
				else
					selectedHai = naniwokiru(tehai, arKawa[selectPlayer], getBafuHai(), getJifuHai(selectPlayer), dorahyouji, getGenbutsu(selectPlayer), getVisiblePai(selectPlayer));
			}
		}
	}
	//予定されたアクションの実行
	if (action == "tsumo")
	{
		s += showStatus(tsumo, "", selectPlayer, selectPlayer, {0, 0, 0}, "", 0, -1);
		//発声通知
		s += notifyAll({"say", playerName[selectPlayer], "tsumo"});
		string si;
		string animNo;
		animNo = "5000001";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		animNo = "5001999";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		arSavedWordCommand += {"Menu_NextTurn"};
		s += si;
		s += "\![unlock,balloonrepaint]\![unlock,repaint]";
		s += "\x\![raise,OnTsumoScore," + selectPlayer + "]";
		s += "\e";
		isLockedSingleClick = 0;
		reservedClickScript = "\![raise,OnTsumoScore," + selectPlayer + "]";
		return s;
	}
	else if (action == "ankan")
	{
		string ankanHai = selectedHai;
		arTehai[selectPlayer] = tehai;
		isRinshanChance = 1;
		setAnkan(selectPlayer, ankanHai);
		string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
		dorahyouji += strDorahyoujiNew;
		visiblePai += strDorahyoujiNew;
		tsumo = arYama[nYamaIndex++];
		s += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, "", 0, -1);
		s += "\_q";
		s += "\![unlock,balloonrepaint]\![unlock,repaint]";
		string sr;
		//発声通知
		sr += notifyAll({"say", playerName[selectPlayer], "kan"});
		//晒した牌通知
		sr += notifyAll({"open", playerName[selectPlayer], ankanHai + ankanHai + ankanHai + ankanHai});
		//ツモ通知
		if (selectPlayer != 3)
			sr += notifyByIndex(selectPlayer, {"tsumo", playerName[selectPlayer], getLeftYama(), tsumo});
		//dora通知
		sr += notifyAll({"dora", strDorahyoujiNew});
		//捨て牌問い合わせ
		if (selectPlayer != 3)
		{
			sr += notifyByIndex(selectPlayer, {"sutehai?"});
		}
		if (selectPlayer == 3)
		{
			lastScript = s + "\e";
			s += sr;
		}
		else
		{
			s += sr;
			s += "\![notify,OnNotifyPrepareSuteSelect," + selectPlayer + "," + tsumo + "]";
		}
		return s + "\e";
	}
	else if (action == "kakan")
	{
		string kakanHai = selectedHai;
		arTehai[selectPlayer] = tehai;
		isRinshanChance = 1;
		setKakan(selectPlayer, kakanHai);
		string sr;
		//発声通知
		sr += notifyAll({"say", playerName[selectPlayer], "kan"});
		//晒した牌通知
		sr += notifyAll({"open", playerName[selectPlayer], kakanHai});
		//この時点でロン(槍槓)を受け付ける必要がある
//		s += showStatus("", kakanHai, selectPlayer, -1, {0, 0, 0}, kakanHai, 1, -1);
//		if (selectPlayer != 3)
//		{
//			if (canRon(3, kakanHai))
//			{
//				s += "\![unlock,balloonrepaint]\![unlock,repaint]";
//				s += sr + "\x\0\b[2]";
//				sr = "";
//				isLockedSingleClick = 0;
//			}
//			else
//			{
//				isLockedSingleClick = 1;
//			}
//		}
		isRecievedResponse = 1;
		string rcs = "";
		for (int i = 0; i < 3; i++)
		{
			if (i != selectPlayer)
			{
				if (canRon(i, kakanHai))
					rcs += notifyByIndex(i, {"naku?", kakanHai, "ron"});
			}
		}
		if (rcs != "")
		{
			rcs += "\![notify,OnNotifyPrepareSuteComplete," + selectPlayer + "," + kakanHai + "]";
			reservedClickScript = rcs;
			s += showStatus("", kakanHai, selectPlayer, -1, {0, 0, 0}, kakanHai, 1, -1);
			s += rcs + "\_q";
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			return s + sr + "\e";//ロンされなかったらどうしよう
		}
		string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
		dorahyouji += strDorahyoujiNew;
		visiblePai += strDorahyoujiNew;
		tsumo = arYama[nYamaIndex++];
		rcs += showStatus(tsumo, "", selectPlayer, -1, {0, 0, 0}, kakanHai, 0, -1);
		rcs += "\_q";
		rcs += "\![unlock,balloonrepaint]\![unlock,repaint]";
		if (selectPlayer != 3)
		{
			//ツモ通知
			sr += notifyByIndex(selectPlayer, {"tsumo", playerName[selectPlayer], getLeftYama(), tsumo});
			//捨て牌問い合わせ
			sr += notifyByIndex(selectPlayer, {"sutehai?"});
		}
		//dora通知
		sr += notifyAll({"dora", strDorahyoujiNew});
		if (selectPlayer == 3)
		{
			lastScript = s + rcs + "\e";
			s += rcs + sr;
		}
		else
		{
			s += rcs + sr;
			s += "\![notify,OnNotifyPrepareSuteSelect," + selectPlayer + "," + tsumo + "]";
			reservedClickScript = rcs + sr;
		}
		return s + "\e";
	}
	else if (action == "richi")
	{
		sute = selectedHai;
		arRichiJunme[selectPlayer] = _aryvn(arKawa[selectPlayer]);
		//発声通知
		s += notifyAll({"say", playerName[selectPlayer], "richi"});
	}
	else
	{
		sute = selectedHai;
	}
	isRinshanChance = 0;
	setSutehai(sute, selectPlayer);
	arTehai[selectPlayer] = removeHai(tehai, sute);
	int isFuroChance = 0;
	int furoChancePlayer = -1;
	if (canRon(3, sute) || canPon(3, sute) || (selectPlayer + 1 == 3) && canChi(3, sute))
	{
		isFuroChance = 1;
		furoChancePlayer = selectPlayer;
	}
	s += showStatus("", sute, selectPlayer, -1, {0, 0, 0}, "", 0, furoChancePlayer);
	//捨て牌通知
	s += notifyAll({"sutehai", playerName[selectPlayer], sute});
	//userが鳴く、ロンするのが可能な場合、\xを挟んで一時停止する canPonはcanDaiminkanを兼ねる
	if (selectPlayer != 3)
	{
		if (isFuroChance)
		{
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			s += "\x[noclear]";
			isLockedSingleClick = 0;
		}
		else
		{
			isLockedSingleClick = 1;
		}
	}
	isRecievedResponse = 1;
	string rcs = "";
	for (int i = 0; i < 3; i++)
	{
		if (i != selectPlayer)
		{
			array command = {"naku?"};
			if (canRon(i, sute))
				command += "ron";
			if (canPon(i, sute))
				command += "pon";
			if ((selectPlayer + 1) % 4 == i && canChi(i, sute))
				command += "chi";
			if (canDaiminkan(i, sute))
				command += "kan";
			if (_aryvn(command) > 1)
				//鳴くか問い合わせ
				rcs += notifyByIndex(i, command);
		}
	}
	rcs += "\![notify,OnNotifyPrepareSuteComplete," + selectPlayer + "," + sute + "]";
	reservedClickScript = rcs;
	s += rcs + "\_q";
	s += "\![unlock,balloonrepaint]\![unlock,repaint]";
	s += "\e";
	return s;
}

string OnNotifyPrepareSuteSelect(dict ref)
{
	int selectPlayer = ref["Reference0"];
	string tsumo = ref["Reference1"];
	reservedScript = "\C\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]";
	_create_thread("TH_WaitResponse");
	return "";
}

string OnNotifyPrepareSuteComplete(dict ref)
{
	int selectPlayer = ref["Reference0"];
	string sute = ref["Reference1"];
	reservedScript = "\C\![raise,OnSuteComplete," + selectPlayer + "," + sute + "]";
	_create_thread("TH_WaitResponse");
	return "";
}

string OnSuteComplete(dict ref)
{
	int completePlayer = ref["Reference0"];
	string sute = ref["Reference1"];
	string s = "\C\![lock,balloonrepaint]\![lock,repaint]\c\![set,balloontimeout,0]\![set,choicetimeout,0]\0\b[2]\_q";
	dict reservedNakuCopy;
	reservedNakuCopy = ${
		$(playerName[0], {}),
		$(playerName[1], {}),
		$(playerName[2], {}),
		$(playerName[3], {})
	};
	for (int i = 0; i <= 3; i++)
	{
		string action = reservedNaku[playerName[i]][0];
		if (action != "")
			reservedNakuCopy[playerName[i]] = reservedNaku[playerName[i]];
	}
	reservedNaku = ${
		$(playerName[0], {}),
		$(playerName[1], {}),
		$(playerName[2], {}),
		$(playerName[3], {})
	};
	//レスポンスが無かったプレイヤーの行動の補完
	array arActionPlayer = {};
	for (int i = 1; i <= 3; i++)
	{
		int index = (i + completePlayer) % 4;
		arActionPlayer += index;
	}
	for (int x = 0; arActionPlayer[x] != nil; x++)
	{
		int i = arActionPlayer[x];
		string action = reservedNakuCopy[playerName[i]][0];
		if (action == "" && i != 3)
		{
			if (canRon(i, sute))
			{
				reservedNakuCopy[playerName[i]] = {"ron"};
			}
			else if (shouldPon(i, sute))
			{
				reservedNakuCopy[playerName[i]] = {"pon"};
			}
			else if ((completePlayer + 1) % 4 == i && shouldChi(i, sute))
			{
				array a = getChiMaterialBest(i, sute);
				reservedNakuCopy[playerName[i]] = {"chi", a[0], a[1]};
			}
			else if (shouldDaiminkan(i, sute))
			{
				reservedNakuCopy[playerName[i]] = {"kan"};
			}
		}
	}
	//副露の優先順位を考慮
	int p = -1;
	int isTripleRon = 0;
	if (reservedNakuCopy[playerName[arActionPlayer[0]]][0] == "ron"
		&& reservedNakuCopy[playerName[arActionPlayer[1]]][0] == "ron"
		&& reservedNakuCopy[playerName[arActionPlayer[2]]][0] == "ron")
	{
		isTripleRon = 1;
	}
	else
	{
		array arActions = {"ron", "pon", "kan", "chi"};
		for (int j = 0; arActions[j] != nil; j++)
		{
			for (int x = 0; arActionPlayer[x] != nil; x++)
			{
				int i = arActionPlayer[x];
				string action = reservedNakuCopy[playerName[i]][0];
				if (p >= 0 && p != i && action != "")
					reservedNakuCopy[playerName[i]] = {};//優先順位の低いアクションは全てキャンセルする
				else if (action == arActions[j])
					p = i;
			}
		}
	}
	//予定されたアクションの実行
	if (p >= 0)
	{
		string action = reservedNakuCopy[playerName[p]][0];
		if (action == "ron")
		{
			s += showStatus("", sute, completePlayer, p, {0, 0, 0}, "", 0, -1);
			//発声通知
			s += notifyAll({"say", playerName[p], "ron"});
			string si;
			string animNo;
			animNo = "5000001";
			si += "\![anim,offset," + animNo + ",0,0]";
			si += "\i[" + animNo + "]";
			arSavedTehai[3] += animNo;
			animNo = "5001999";
			si += "\![anim,offset," + animNo + ",0,0]";
			si += "\i[" + animNo + "]";
			arSavedTehai[3] += animNo;
			arSavedWordCommand += {"Menu_NextTurn"};
			s += si;
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			s += "\x\![raise,OnRonScore," + p + "," + completePlayer + "," + sute + "]";
			s += "\e";
			isLockedSingleClick = 0;
			reservedClickScript = "\![raise,OnRonScore," + p + "," + completePlayer + "," + sute + "]";
			return s;
		}
		if (action == "pon")
		{
			setKyotaku(completePlayer);
			setFuro(p, completePlayer, sute, sute + sute);
			s += showStatus("", "", p, -1, {0, 0, 0}, sute + sute, 0, -1);
			//発声通知
			s += notifyAll({"say", playerName[p], "pon"});
			//晒した牌通知
			s += notifyAll({"open", playerName[p], sute + sute + sute});
			//捨て牌問い合わせ
			if (p != 3)
			{
				s += notifyByIndex(p, {"sutehai?"});
				s += "\![notify,OnNotifyPrepareSuteSelect," + p + ",]";
			}
			else
			{
				isLockedSingleClick = 0;
			}
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			s += "\e";
			return s;
		}
		if (action == "chi")
		{
			string hai1 = reservedNakuCopy[playerName[p]][1];
			string hai2 = reservedNakuCopy[playerName[p]][2];
			string furoHai = _arystr(sortHai(strHaiToArray(sute + hai1 + hai2)));
			setKyotaku(completePlayer);
			setFuro(p, completePlayer, sute, hai1 + hai2);
			s += showStatus("", "", p, -1, {0, 0, 0}, hai1 + hai2, 0, -1);
			//発声通知
			s += notifyAll({"say", playerName[p], "chi"});
			//晒した牌通知
			s += notifyAll({"open", playerName[p], furoHai});
			//捨て牌問い合わせ
			if (p != 3)
			{
				s += notifyByIndex(p, {"sutehai?"});
				s += "\![notify,OnNotifyPrepareSuteSelect," + p + ",]";
			}
			else
			{
				isLockedSingleClick = 0;
			}
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			s += "\e";
			return s;
		}
		if (action == "kan")
		{
			isRinshanChance = 1;
			setKyotaku(completePlayer);
			setFuro(p, completePlayer, sute, sute + sute + sute);
			string strDorahyoujiNew = arYama[52 + (_strlen(dorahyouji) / 2)];
			dorahyouji += strDorahyoujiNew;
			visiblePai += strDorahyoujiNew;
			string tsumo = arYama[nYamaIndex++];
			s += showStatus(tsumo, "", p, -1, {0, 0, 0}, sute + sute + sute, 0, -1);
			//発声通知
			s += notifyAll({"say", playerName[p], "kan"});
			//晒した牌通知
			s += notifyAll({"open", playerName[p], sute + sute + sute + sute});
			if (p != 3)
			{
				//ツモ通知
				s += notifyByIndex(p, {"tsumo", playerName[p], getLeftYama(), tsumo});
				//捨て牌問い合わせ
				s += notifyByIndex(p, {"sutehai?"});
				s += "\![notify,OnNotifyPrepareSuteSelect," + p + "," + tsumo + "]";
			}
			else
			{
				isLockedSingleClick = 0;
			}
			//dora通知
			s += notifyAll({"dora", strDorahyoujiNew});
			s += "\_q";
			s += "\![unlock,balloonrepaint]\![unlock,repaint]";
			s += "\e";
			return s;
		}
	}
	string reason = "";
	//四風子連打
	if (nYamaIndex == 70 && arChihouChance[completePlayer] == 1)
	{
		if (
			(sute == "1z" || sute == "2z" || sute == "3z" || sute == "4z")
			&& (arKawa[0][0] == arKawa[1][0] && arKawa[0][0] == arKawa[2][0] && arKawa[0][0] == arKawa[3][0])
		)
			reason = "4renda";
	}
	//四開槓
	if (countKantsu(completePlayer) > 0)
	{
		if (countKantsu(0) + countKantsu(1) + countKantsu(2) + countKantsu(3) == 4 && countKantsu(completePlayer) != 4)
			reason = "4kan";
	}
	//四家立直
	if (arRichiJunme[0] >= 0 && arRichiJunme[1] >= 0 && arRichiJunme[2] >= 0 && arRichiJunme[3] >= 0)
	{
		reason = "4richi";
	}
	//三家和
	if (isTripleRon)
	{
		reason = "3ron";
		//発声通知
		for (int x = 0; x < 3; x++)
		{
			s += notifyAll({"say", playerName[arActionPlayer[x]], "ron"});
		}
	}
	//途中流局
	if (reason != "")
	{
		array arTenpaiPlayerFlag = {0, 0, 0};
		if (reason == "3ron")
		{
			arTenpaiPlayerFlag = {1, 1, 1, 1};
			arTenpaiPlayerFlag[completePlayer] = 0;
		}
		else if (reason == "4richi")
		{
			setKyotaku(completePlayer);
		}
		isLockedSingleClick = 0;
		s += showStatus("", "", -1, -1, arTenpaiPlayerFlag, "", 0, -1);
		string si;
		string animNo;
		animNo = "5000001";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		animNo = "5001999";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		arSavedWordCommand += {"Menu_NextTurn"};
		s += si;
		s += "\![unlock,balloonrepaint]\![unlock,repaint]";
		s += "\x[noclear]";
		string rcs = "";
		rcs += "\![raise,OnRyukyokuScore," + reason + "," + _arystr(arTenpaiPlayerFlag) + "]";
		reservedClickScript = rcs;
		s += rcs + "\_q\e";
		return s;
	}
	//流局(荒牌平局)
	if (_aryvn(arYama) == nYamaIndex)
	{
		isLockedSingleClick = 0;
		s += showStatus("", "", -1, -1, {0, 0, 0}, "", 0, -1);
		string si;
		string animNo;
		animNo = "5000001";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		animNo = "5001999";
		si += "\![anim,offset," + animNo + ",0,0]";
		si += "\i[" + animNo + "]";
		arSavedTehai[3] += animNo;
		arSavedWordCommand += {"Menu_NextTurn"};
		s += si;
		s += "\![unlock,balloonrepaint]\![unlock,repaint]";
		s += "\x[noclear]";
		string rcs = "";
		//流し満貫判定
		string strYaochu = "1m9m1p9p1s9s1z2z3z4z5z6z7z";
		for (int i = 0; i < 4; i++)
		{
			if (_aryvn(arFuroJunme[i]) == 0)//鳴かれてない
			{
				int isNagashimangan = 1;
				for (int j = 0; arKawa[i][j] != nil; j++)
				{
					if (_strstr(strYaochu, arKawa[i][j], 0) == -1)
					{
						isNagashimangan = 0;
						break;
					}
				}
				if (isNagashimangan)
				{
					rcs += "\![notify,OnNotifyPrepareRyukyoku,nagashimangan," + i + "]";
					reservedClickScript = rcs;
					s += rcs + "\_q\e";
					return s;
				}
			}
		}
		//テンパイ判定
		isRecievedResponse = 1;
		for (int i = 0; i < 3; i++)
		{
			int shanten = getShanten(arTehai[i]);
			if (shanten == 0)
			{
				//テンパイ宣言問い合わせ
				rcs += notifyByIndex(i, {"tenpai?"});
			}
		}
		rcs += "\![notify,OnNotifyPrepareRyukyoku,kouhai]";
		reservedClickScript = rcs;
		s += rcs + "\_q\e";
		return s;
	}
	setKyotaku(completePlayer);
	int nextPlayer = (completePlayer + 1) % 4;
	string tsumo;
	//userがツモ
	if (nextPlayer == 3)
	{
		isLockedSingleClick = 0;
		isLockedDoubleClick = 0;
		tsumo = arYama[nYamaIndex++];
		s += showStatus(tsumo, "", 3, -1, {0, 0, 0}, "", 0, -1);
		s += "\_q";
		s += "\![unlock,balloonrepaint]\![unlock,repaint]";
		s += "\e";
		lastScript = s;
		return s;
	}
	//user以外がツモ
	isLockedSingleClick = 1;
	isLockedDoubleClick = 1;
	tsumo = arYama[nYamaIndex++];
	s += showStatus(tsumo, "", nextPlayer, -1, {0, 0, 0}, "", 0, -1);
	//ツモ通知
	s += notifyByIndex(nextPlayer, {"tsumo", playerName[nextPlayer], getLeftYama(), tsumo});
	//捨て牌問い合わせ
	s += notifyByIndex(nextPlayer, {"sutehai?"});
	s += "\_q";
	s += "\![unlock,balloonrepaint]\![unlock,repaint]";
	s += "\![notify,OnNotifyPrepareSuteSelect," + nextPlayer + "," + tsumo + "]";
	s += "\e";
	return s;
}

int countKantsu(int nPlayer)
{
	int count = 0;
	array arHai = strHaiToArrayWithFuro(arTehai[nPlayer]);
	for (int i = 0; arHai[i] != nil; i++)
	{
		if (arHai[i][0] == "furo" && _strlen(arHai[i][1]) == 8)
			count++;
		else if (arHai[i][0] == "ankan")
			count++;
	}
	return count;
}

setKyotaku(int nPlayer)
{
	if (arRichiJunme[nPlayer] == _aryvn(arKawa[nPlayer]) - 1)
	{
		kyotaku++;
		arScore[nPlayer] -= 1000;
		arIppatsuChance[nPlayer] = 1;
		if (arChihouChance[nPlayer])
			arWRichi[nPlayer] = 1;
	}
	else
	{
		arIppatsuChance[nPlayer] = 0;
	}
	arChihouChance[nPlayer] = 0;
}

string OnNotifyPrepareRyukyoku(dict ref)
{
	reservedScript = "\C\0\b[2]\![raise,OnRyukyoku," + ref["Reference0"] + "," + ref["Reference1"] + "]";
	_create_thread("TH_WaitResponse");
	return "";
}

string OnRyukyoku(dict ref)
{
	string reason = ref["Reference0"];
	if (reason == "nagashimangan")
		return "\![raise,OnRyukyokuScore," + reason + "," + ref["Reference1"] + "]\e";
	if (reason != "kouhai")
		return "\![raise,OnRyukyokuScore," + reason + "]\e";
	string m;
	array arTenpaiPlayerFlag = {0, 0, 0};
	for (int i = 0; i < 3; i++)
	{
		string action = reservedTenpai[playerName[i]];
		if (action == "")
		{
			int shanten = getShanten(arTehai[i]);
			if (shanten == 0 && shouldSayTenpai(i))
				action = "yes";
			else
				action = "no";
		}
		if (action != "yes" && arRichiJunme[i] >= 0)//リーチかけてたらテンパイ宣言させる
		{
			action = "yes";
			//指定されたアクションが不可能だった
			senderrorlog("warning", "failed requested action '" + action + "' in 'tenpai?' command.");
		}
		if (action == "yes")
		{
			arTenpaiPlayerFlag[i] = 1;
			m += notifyAll({"say", playerName[i], "tenpai"});
		}
		else
		{
			arTenpaiPlayerFlag[i] = 0;
			m += notifyAll({"say", playerName[i], "noten"});
		}
	}
	int shanten = getShanten(arTehai[3]);
	if (shanten == 0)
		arTenpaiPlayerFlag += 1;
	else
		arTenpaiPlayerFlag += 0;
	string s = "\![set,balloontimeout,0]\0\b[2]\_q";
	s += showStatus("", "", -1, -1, arTenpaiPlayerFlag, "", 0, -1);
	s += m;
	string si;
	string animNo;
	animNo = "5000001";
	si += "\![anim,offset," + animNo + ",0,0]";
	si += "\i[" + animNo + "]";
	arSavedTehai[3] += animNo;
	animNo = "5001999";
	si += "\![anim,offset," + animNo + ",0,0]";
	si += "\i[" + animNo + "]";
	arSavedTehai[3] += animNo;
	arSavedWordCommand += {"Menu_NextTurn"};
	s += si;
	s += "\x\![raise,OnRyukyokuScore," + reason + "," + _arystr(arTenpaiPlayerFlag) + "]";
	s += "\e";
	isLockedSingleClick = 0;
	reservedClickScript = "\![raise,OnRyukyokuScore," + reason + "," + _arystr(arTenpaiPlayerFlag) + "]";
	return s;
}

setSutehai(string sute, int nPlayer)
{
	arKawa[nPlayer] += sute;
	visiblePai += sute;
	for (int i = 0; i < 4; i++)
	{
		if (arRichiJunme[i] >= 0)
			arFuritenCheckRichi[i] += sute;
		if (nPlayer == i)
			arFuritenCheckTurn[i] = {};
		else
			arFuritenCheckTurn[i] += sute;
	}
}

string click_p01(dict ref) { return getSutehaiFromClick(ref); }
string click_p02(dict ref) { return getSutehaiFromClick(ref); }
string click_p03(dict ref) { return getSutehaiFromClick(ref); }
string click_p04(dict ref) { return getSutehaiFromClick(ref); }
string click_p05(dict ref) { return getSutehaiFromClick(ref); }
string click_p06(dict ref) { return getSutehaiFromClick(ref); }
string click_p07(dict ref) { return getSutehaiFromClick(ref); }
string click_p08(dict ref) { return getSutehaiFromClick(ref); }
string click_p09(dict ref) { return getSutehaiFromClick(ref); }
string click_p10(dict ref) { return getSutehaiFromClick(ref); }
string click_p11(dict ref) { return getSutehaiFromClick(ref); }
string click_p12(dict ref) { return getSutehaiFromClick(ref); }
string click_p13(dict ref) { return getSutehaiFromClick(ref); }
string click_p14(dict ref) { return getSutehaiFromClick(ref); }
string getSutehaiFromClick(dict ref)
{
	if (isLockedSingleClick)
		return "";
	isLockedSingleClick = 1;
	isLockedDoubleClick = 1;
	int i = (int)_substr(ref["Reference4"], 1, 2);
	string sute;
	string tsumo = savedTsumo;
	savedTsumo = "";
	if (i <= 13)
		sute = _substr(arTehai[3], 2 * (i - 1), 2);
	else
		sute = tsumo;
	string animNo;
	string si;
	animNo = "40" + fillzero(i) + "0" + getSurfaceCode(sute);
	si += "\![anim,clear," + animNo + "]";
	animNo = "40" + fillzero(i) + "999";
	si += "\![anim,clear," + animNo + "]";
	string s = "\![raise,OnChoiceSelectEx,,Menu_Sutehai," + sute + "," + tsumo + "]";
	return "\C\![lock,balloonrepaint]\![lock,repaint]\0\b[2]" + si + s + "\![unlock,balloonrepaint]\![unlock,repaint]\e";
}

string Menu_Sutehai(dict ref)
{
	isLockedSingleClick = 1;
	isLockedDoubleClick = 1;
	int selectPlayer = 3;
	string sute = ref["Reference2"];
	string tsumo = ref["Reference3"];
	reservedSelectedHai = sute;
	return "\C\0\b[2]\![raise,OnSuteSelect," + selectPlayer + "," + tsumo + "]\e";
}

string clearAnimation()
{
	string si;
	for (int p = 0; p <= 3; p++)
	{
		for (int i = 0; arSavedTehai[p][i] != nil; i++)
		{
			si += "\![anim,clear," + arSavedTehai[p][i] + "]";
		}
		arSavedTehai[p] = {};
	}
	for (int i = 0; i < 14; i++)
	{
		string animNo = "40" + fillzero(i + 1) + "999";
		si += "\![anim,clear," + animNo + "]";
	}
	return si;
}

string showStatus(string tsumo, string sute, int nPlayer, int agariPlayer, array arTenpaiPlayerFlag, string openedHai, int isKakan, int furoChancePlayer)
{
	string s = "";
	string si = "";
	string siInfo = "";
	string sClear = clearAnimation();
	array savedTehai = {};
	string animNo;
	int offset = 0;
	array wanpai;
	array arDorahyouji = strHaiToArray(dorahyouji);
	int isMinkan = 0;
	if (openedHai != "" && tsumo != "")
		isMinkan = 1;
	for (int i = 0; i < 5; i++)
	{
		if (arDorahyouji[i] == nil)
		{
			wanpai += "tb";
		}
		else
		{
			if (isMinkan && i == _aryvn(arDorahyouji) - 1)
			{
				wanpai += "tb";
			}
			else
			{
				wanpai += arDorahyouji[i];
			}
		}
	}
	array seki = {"東", "南", "西", "北"};
	dict dSeki;
	array pNames;
	for (int i = 0; playerName[i] != nil; i++)
	{
		pNames += playerName[(i + oyaIndex) % 4];
	}
	for (int i = 0; pNames[i] != nil; i++)
	{
		dSeki += $(pNames[i], seki[i]);
	}
	//王牌
	if (!modeEx)
	{
		s += "\f[height,+16]\_l[110,-15]";
		s += convertUnicode("tb") + convertUnicode("tb");
	}
	for (int i = 0; wanpai[i] != nil; i++)
	{
		if (!modeEx)
			s += convertUnicode(wanpai[i]);
		animNo = "600" + (i + 1) + "0" + getSurfaceCode(wanpai[i]);
		siInfo += "\i[" + animNo + "]";
		savedTehai += animNo;
	}
	if (modeEx)
	{
		s += "\_l[350,370]" + arBafu[bafu] + kyoku + "局";
	}
	else
	{
		s += "\f[height,default]";
		//場風、局数、積み棒、供託棒
		s += "\_l[,0] " + arBafu[bafu] + kyoku + "局";
		s += "\_l[260,0]●x" + tsumibou + "\n";
		s += "\_l[260,]\f[color,red]●\f[color,default]x" + kyotaku;
	}
	int n = tsumibou;
	if (n > 9)
		n = 9;
	for (int i = 0; i < n; i++)
	{
		animNo = "600001" + (i + 1);
		siInfo += "\i[" + animNo + "]";
		savedTehai += animNo;
	}
	n = kyotaku;
	if (n > 9)
		n = 9;
	for (int i = 0; i < n; i++)
	{
		animNo = "600002" + (i + 1);
		siInfo += "\i[" + animNo + "]";
		savedTehai += animNo;
	}
	//0-2
	for (int p = 0; p < 3; p++)
	{
		if (modeEx)
		{
			if (p == 0)
			{
				s += "\_l[550,250]" + dSeki[playerName[p]] + "家<<" + playerName[p] + ">>";
				s += "\n\_l[550,]" + arScore[p] + "点";
			}
			else if (p == 1)
			{
				s += "\_l[250,100]" + dSeki[playerName[p]] + "家<<" + playerName[p] + ">>";
				s += "\n\_l[250,]" + arScore[p] + "点";
			}
			else if (p == 2)
			{
				s += "\_l[100,500]" + dSeki[playerName[p]] + "家<<" + playerName[p] + ">>";
				s += "\n\_l[100,]" + arScore[p] + "点";
			}
		}
		else
		{
			s += "\_l[0," + (80 * p) + "]\f[height,default]" + dSeki[playerName[p]] + "家<<" + playerName[p] + ">>\n";
			s += "\f[height,+16]";
		}
		if (openTehai || (p == agariPlayer) || arTenpaiPlayerFlag[p])
		{
			string tsumoShowing = "";
			if (nPlayer == p && tsumo != "")
				tsumoShowing = tsumo;
			s += showHai(arTehai[p], tsumoShowing, p, nPlayer, "", 1);
		}
		else
		{
			string tsumoShowing = "";
			if (nPlayer == p && tsumo != "")
				tsumoShowing = tsumo;
			s += showHai(arTehai[p], tsumoShowing, p, nPlayer, "", 0);
		}
		s += "\n";
		int isFuroChance = 0;
		if (p == furoChancePlayer)
			isFuroChance = 1;
		s += showKawa(nPlayer, p, agariPlayer, isFuroChance);
	}
	//3
	int pUser = 3;
	if (modeEx)
	{
		s += "\_l[500,630]" + dSeki[playerName[pUser]] + "家<<" + playerName[pUser] + ">>";
		s += "\n\_l[500,]" + arScore[pUser] + "点";
	}
	else
	{
		s += "\_l[0,240]\f[height,default]" + dSeki[playerName[pUser]] + "家<<" + playerName[pUser] + ">>\n";
	}
	s += "\f[height,+16]";
	s += showKawa(nPlayer, pUser, agariPlayer, 0);
	s += "\n";
	string tsumoShowing = "";
	if (nPlayer == pUser)
		tsumoShowing = tsumo;
	s += showHai(arTehai[pUser], tsumoShowing, pUser, nPlayer, openedHai, 1);
	s += "\f[height,default]";
	int n = getLeftYama();
	if (n > 0 || tsumo != "")
	{
		if (modeEx)
			s += "\_l[350,390]残り: " + n + "枚";
		else
			s += "\n\n\_l[225,]残り: " + n + "枚";
	}
	else
	{
		if (modeEx)
			s += "\_l[350,390]流局";
		else
			s += "\n\n\_l[225,]流局";
	}
	s += "\_l[0,]";
	int nWord = 1;
	arSavedWordCommand = {};
	if (nPlayer == pUser && tsumo != "")
	{
		if (canTouhai(pUser, tsumo))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Touhai,3]倒牌\__q";
			animNo = "5000017";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Touhai", 3};
		}
		if (canTsumo(pUser, tsumo))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Tsumo,3]ツモ\__q";
			animNo = "5000012";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Tsumo", 3};
		}
		if (canRichi(pUser, tsumo))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Richi]リーチ\__q";
			animNo = "5000011";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Richi"};
		}
		if (canKakan(pUser, tsumo) || canAnkan(pUser, tsumo))
		{
			array arKakanHai = getKakanHai(addHai(arTehai[pUser], tsumo));
			array arAnkanHai;
			if (arRichiJunme[pUser] == -1)
				arAnkanHai = getAnkanHai(addHai(arTehai[pUser], tsumo));
			else if (arRichiJunme[pUser] >= 0 && canAnkan(pUser, tsumo))
				arAnkanHai = {tsumo};
			if (_aryvn(arKakanHai) + _aryvn(arAnkanHai) == 1)
			{
				array saveCommand;
				if (_aryvn(arKakanHai) == 1)
				{
					if (!modeEx)
						s += "\![*]\__q[Menu_Kakan,3," + arKakanHai[0] + "]カン\__q";
					saveCommand = {"Menu_Kakan", 3, arKakanHai[0]};
				}
				else
				{
					if (!modeEx)
						s += "\![*]\__q[Menu_Ankan,3," + arAnkanHai[0] + "]カン\__q";
					saveCommand = {"Menu_Ankan", 3, arAnkanHai[0]};
				}
				animNo = "5000016";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				animNo = "500" + (nWord++) + "999";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				offset += 110;
				arSavedWordCommand += saveCommand;
			}
			else
			{
				if (!modeEx)
					s += "\![*]カン";
				animNo = "5000016";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				offset += 110;
				int nPaiIndex = 1;
				for (int i = 0; arKakanHai[i] != nil; i++)
				{
					if (!modeEx)
						s += " \__q[Menu_Kakan,3," + arKakanHai[i] + "]\f[height,+16]" + convertUnicode(arKakanHai[i]) + "\f[height,default]\__q";
					animNo = "500" + (nPaiIndex++) + "0" + getSurfaceCode(arKakanHai[i]);
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					animNo = "501" + (nWord++) + "999";
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					offset += 42;
					arSavedWordCommand += {"Menu_Kakan", 3, arKakanHai[i]};
				}
				for (int i = 0; arAnkanHai[i] != nil; i++)
				{
					if (!modeEx)
						s += " \__q[Menu_Ankan,3," + arAnkanHai[i] + "]\f[height,+16]" + convertUnicode(arAnkanHai[i]) + "\f[height,default]\__q";
					animNo = "500" + (nPaiIndex++) + "0" + getSurfaceCode(arAnkanHai[i]);
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					animNo = "501" + (nWord++) + "999";
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					offset += 42;
					arSavedWordCommand += {"Menu_Ankan", 3, arAnkanHai[i]};
				}
			}
		}
	}
	if (nPlayer != pUser && sute != "" && agariPlayer == -1)
	{
		if (canRon(pUser, sute))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Ron,3," + nPlayer + "," + sute + "]ロン\__q";
			animNo = "5000013";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Ron", 3, nPlayer, sute};
		}
		if (canPon(pUser, sute))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Pon,3," + nPlayer + "," + sute + "]ポン\__q";
			animNo = "5000015";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Pon", 3, nPlayer, sute};
		}
		if (nPlayer + 1 == pUser && canChi(pUser, sute) && !isKakan)
		{
			array arMaterial = getChiMaterial(arTehai[pUser], sute);
			if (_aryvn(arMaterial) == 1)
			{
				if (!modeEx)
					s += "\![*]\__q[Menu_Chi,3," + nPlayer + "," + sute + "," + arMaterial[0] + "]チー\__q";
				animNo = "5000014";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				animNo = "500" + (nWord++) + "999";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				offset += 110;
				arSavedWordCommand += {"Menu_Chi", 3, nPlayer, sute, arMaterial[0]};
			}
			else
			{
				if (!modeEx)
					s += "\![*]チー";
				animNo = "5000014";
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
				offset += 110;
				int nPaiIndex = 1;
				for (int i = 0; arMaterial[i] != nil; i++)
				{
					array arMaterialSelected = strHaiToArray(arMaterial[i]);
					string hai1 = arMaterialSelected[0];
					string hai2 = arMaterialSelected[1];
					if (!modeEx)
						s += " \__q[Menu_Chi,3," + nPlayer + "," + sute + "," + arMaterial[i] + "]\f[height,+16]" + convertUnicode(hai1) + convertUnicode(hai2) + "\f[height,default]\__q";
					animNo = "500" + (nPaiIndex++) + "0" + getSurfaceCode(hai1);
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					animNo = "501" + (nWord++) + "999";
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					offset += 32;
					arSavedWordCommand += {"Menu_Chi", 3, nPlayer, sute, arMaterial[i]};
					animNo = "500" + (nPaiIndex++) + "0" + getSurfaceCode(hai2);
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					animNo = "501" + (nWord++) + "999";
					si += "\![anim,offset," + animNo + "," + offset + ",0]";
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
					offset += 42;
					arSavedWordCommand += {"Menu_Chi", 3, nPlayer, sute, arMaterial[i]};
				}
			}
		}
		if (canDaiminkan(pUser, sute))
		{
			if (!modeEx)
				s += "\![*]\__q[Menu_Daiminkan,3," + nPlayer + "," + sute + "]カン\__q";
			animNo = "5000016";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			offset += 110;
			arSavedWordCommand += {"Menu_Daiminkan", 3, nPlayer, sute};
		}
		//次へ
		if (si != "")
		{
			animNo = "5000001";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			animNo = "500" + (nWord++) + "999";
			si += "\![anim,offset," + animNo + "," + offset + ",0]";
			si += "\i[" + animNo + "]";
			savedTehai += animNo;
			arSavedWordCommand += {"Menu_NextTurn"};
		}
	}
	for (int i = 0; savedTehai[i] != nil; i++)
	{
		arSavedTehai[3] += savedTehai[i];
	}
	if (!isLockedDoubleClick)
	{
		if (modeEx)
		{
			s += "\_l[350,410]\![*]\__q[Menu_CANCEL]閉じる\__q\n";
			s += "\_l[350,]\![*]\__q[Menu_EndGame]対局終了\__q";
		}
		else
		{
			s += "\![*]\__q[Menu_CANCEL]閉じる\__q\n";
			s += "\![*]\__q[Menu_EndGame]対局終了\__q";
		}
	}
	return sClear + siInfo + si + s;
}

string click_w01(dict ref) { return getWordCommandFromClick(ref); }
string click_w02(dict ref) { return getWordCommandFromClick(ref); }
string click_w03(dict ref) { return getWordCommandFromClick(ref); }
string click_w04(dict ref) { return getWordCommandFromClick(ref); }
string click_w05(dict ref) { return getWordCommandFromClick(ref); }
string click_w06(dict ref) { return getWordCommandFromClick(ref); }
string click_w07(dict ref) { return getWordCommandFromClick(ref); }
string click_w08(dict ref) { return getWordCommandFromClick(ref); }
string click_w09(dict ref) { return getWordCommandFromClick(ref); }
string click_w11(dict ref) { return getWordCommandFromClick(ref); }
string click_w12(dict ref) { return getWordCommandFromClick(ref); }
string click_w13(dict ref) { return getWordCommandFromClick(ref); }
string click_w14(dict ref) { return getWordCommandFromClick(ref); }
string click_w15(dict ref) { return getWordCommandFromClick(ref); }
string click_w16(dict ref) { return getWordCommandFromClick(ref); }
string click_w17(dict ref) { return getWordCommandFromClick(ref); }
string click_w18(dict ref) { return getWordCommandFromClick(ref); }
string click_w19(dict ref) { return getWordCommandFromClick(ref); }
string getWordCommandFromClick(dict ref)
{
	if (isLockedSingleClick)
		return "";
	isLockedSingleClick = 1;
	int index = (int)_substr(ref["Reference4"], 2, 1);
	array arWordCommand = arSavedWordCommand[index - 1];
	arSavedWordCommand = {};
	string s = "\![raise,OnChoiceSelectEx,," + arWordCommand[0];
	for (int i = 1; arWordCommand[i] != nil; i++)
	{
		s += "," + arWordCommand[i];
	}
	s += "]";
	string si;
	for (int i = 0; arSavedTehai[3][i] != nil; i++)
	{
		string animNo = arSavedTehai[3][i];
		if (_substr(animNo, 0, 3) == "500")
			si += "\![anim,clear," + animNo + "]";
	}
	return "\0" + si + s + "\e";
}

string Menu_NextTurn(dict ref)
{
	string s = "\0" + reservedClickScript + "\e";
	reservedClickScript = "";
	return s;
}

string Menu_Richi(dict ref)
{
	isLockedSingleClick = 0;
	int p = 3;
	arRichiJunme[p] = _aryvn(arKawa[p]);
	string tsumo = arYama[nYamaIndex - 1];
	string s;
	string sr;
	sr += notifyAll({"say", playerName[p], "richi"});
	s += "\0\b[2]\_q" + showStatus(tsumo, "", p, -1, {0, 0, 0}, "", 0, -1) + "\_q\e";
	lastScript = s;
	return sr + s;
}

string Menu_Kakan(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	string kakanHai = ref["Reference3"];
	string tsumo = arYama[nYamaIndex - 1];
	reservedKakan = 1;
	reservedSelectedHai = kakanHai;
	isLockedSingleClick = 0;
	return "\C\0\b[2]\![raise,OnSuteSelect," + nFuroPlayer + "," + tsumo + "]";
}

string Menu_Ankan(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	string ankanHai = ref["Reference3"];
	string tsumo = arYama[nYamaIndex - 1];
	reservedAnkan = 1;
	reservedSelectedHai = ankanHai;
	isLockedSingleClick = 0;
	return "\C\0\b[2]\![raise,OnSuteSelect," + nFuroPlayer + "," + tsumo + "]";
}

string Menu_Touhai(dict ref)
{
	int nTouhaiPlayer = ref["Reference2"];
	string reason = "touhai";
	array arTenpaiPlayerFlag = {0, 0, 0, 0};
	arTenpaiPlayerFlag[nTouhaiPlayer] = 1;
	return "\C\0\b[2]\![raise,OnRyukyokuScore," + reason + "," + _arystr(arTenpaiPlayerFlag) + "]\e";
}

string Menu_Tsumo(dict ref)
{
	int nAgariPlayer = ref["Reference2"];
	return "\C\0\b[2]\![raise,OnTsumoScore," + nAgariPlayer + "]\e";
}

string Menu_Pon(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	string name = playerName[nFuroPlayer];
	reservedNaku[name] = {"pon"};
	return "\C\0\b[2]\![raise,OnChoiceSelectEx,,Menu_NextTurn]\e";
}

string Menu_Chi(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	string hai = ref["Reference5"];
	array arHai = strHaiToArray(hai);
	string name = playerName[nFuroPlayer];
	reservedNaku[name] = {"chi", arHai[0], arHai[1]};
	return "\C\0\b[2]\![raise,OnChoiceSelectEx,,Menu_NextTurn]\e";
}

string Menu_Daiminkan(dict ref)
{
	int nFuroPlayer = ref["Reference2"];
	int nSutePlayer = ref["Reference3"];
	string sute = ref["Reference4"];
	string name = playerName[nFuroPlayer];
	reservedNaku[name] = {"kan"};
	return "\C\0\b[2]\![raise,OnChoiceSelectEx,,Menu_NextTurn]\e";
}

string Menu_Ron(dict ref)
{
	int nAgariPlayer = ref["Reference2"];
	string name = playerName[nAgariPlayer];
	reservedNaku[name] = {"ron"};
	return "\C\0\b[2]\![raise,OnChoiceSelectEx,,Menu_NextTurn]\e";
}

setFuro(int nFuroPlayer, int nSutePlayer, string sute, string haiUsed)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], haiUsed);
	arTehai[nFuroPlayer] = addFuro(arTehai[nFuroPlayer], sute + haiUsed, "<", ">");
	arFuroJunme[nSutePlayer] += _aryvn(arKawa[nSutePlayer]) - 1;
	arFuroHistory[nFuroPlayer] += {sute, nSutePlayer};
	arIppatsuChance = {0, 0, 0, 0};
	arChihouChance = {0, 0, 0, 0};
	visiblePai += haiUsed;
}

setKakan(int nFuroPlayer, string kakanHai)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], kakanHai);
	arTehai[nFuroPlayer] = _strreplace(arTehai[nFuroPlayer], kakanHai + kakanHai + kakanHai, kakanHai + kakanHai + kakanHai + kakanHai);
	arKakanHistory[nFuroPlayer] += kakanHai;
	arIppatsuChance = {0, 0, 0, 0};
	arChihouChance = {0, 0, 0, 0};
	visiblePai += kakanHai;
}

setAnkan(int nFuroPlayer, string ankanHai)
{
	arTehai[nFuroPlayer] = removeHai(arTehai[nFuroPlayer], ankanHai + ankanHai + ankanHai + ankanHai);
	arTehai[nFuroPlayer] = addFuro(arTehai[nFuroPlayer], ankanHai + ankanHai + ankanHai + ankanHai, "(", ")");
	arIppatsuChance = {0, 0, 0, 0};
	arChihouChance = {0, 0, 0, 0};
	visiblePai += ankanHai + ankanHai + ankanHai + ankanHai;
}

string addFuro(string tehai, string furo, string s1, string s2)
{
	string s;
	string sFuro;
	for (int i = 0; i < _strlen(tehai); i++)
	{
		if (_substr(tehai, i, 1) == "<")
		{
			if (_substr(tehai, i + 7, 1) == ">")
			{
				sFuro += _substr(tehai, i, 8);
				i += 7;
			}
			else if (_substr(tehai, i + 9, 1) == ">")
			{
				sFuro += _substr(tehai, i, 10);
				i += 9;
			}
		}
		else if (_substr(tehai, i, 1) == "(")
		{
			if (_substr(tehai, i + 9, 1) == ")")
			{
				sFuro += _substr(tehai, i, 10);
				i += 9;
			}
		}
		else
		{
			s += _substr(tehai, i, 2);
			i++;
		}
	}
	string strFuro = _arystr(sortHai(strHaiToArray(furo)));
	s = s + s1 + strFuro + s2 + sFuro;
	return s;
}

string Menu_CANCEL(dict ref)
{
	return "\e";
}

clearStatus()
{
	arYama = {};
	arTehai = {"", "", "", ""};
	arKawa = {{}, {}, {}, {}};
	arFuritenCheckRichi = {{}, {}, {}, {}};
	arFuritenCheckTurn = {{}, {}, {}, {}};
	arRichiJunme = {-1, -1, -1, -1};
	arFuroJunme = {{}, {}, {}, {}};
	arFuroHistory = {{}, {}, {}, {}};
	arKakanHistory = {{}, {}, {}, {}};
	isRinshanChance = 0;
	arIppatsuChance = {0, 0, 0, 0};
	arChihouChance = {1, 1, 1, 1};
	arWRichi = {0, 0, 0, 0};
	dorahyouji = "";
	visiblePai = "";
	nYamaIndex = 0;
	lastScript = "";
}

//全牌
array getAllHai()
{
	array a = {
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z",
		"1m", "2m", "3m", "4m", "5m", "6m", "7m", "8m", "9m",
		"1p", "2p", "3p", "4p", "5p", "6p", "7p", "8p", "9p",
		"1s", "2s", "3s", "4s", "5s", "6s", "7s", "8s", "9s",
		"1z", "2z", "3z", "4z", "5z", "6z", "7z"
	};//n = 136
	return a;
}

string showHai(string strHai, string tsumoShowing, int showPlayer, int nPlayer, string openedHai, int isOpen, int isScore)
{
	string s;
	string si;
	array savedTehai = {};
	string animNo;
	array arHai = strHaiToArrayWithFuro(strHai);
	if (showPlayer != 3 || isScore)
	{
		string sFuro;
		int nFuro;
		int nFuroAnim;
		int nFuroOffset;
		int nNormal = 13;
		int offsetX = 32;
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (arHai[i][0] == "furo")
			{
				if (_strlen(arHai[i][1]) == 6)
				{
					offsetX -= 45 + (32 * 2);
				}
				else
				{
					offsetX -= 45 + (32 * 3);
					if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
						offsetX += 32;
				}
			}
			else if (arHai[i][0] == "ankan")
			{
				offsetX -= 32 * 4;
			}
		}
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (arHai[i][0] == "normal")
			{
				string haiCode;
				if (isOpen)
					haiCode = arHai[i][1];
				else
					haiCode = "tb";
				s += convertUnicode(haiCode);
				animNo = "" + (showPlayer + 1) + "0" + fillzero(i + 1) + "0" + getSurfaceCode(haiCode);
				si += "\i[" + animNo + "]";
				savedTehai += animNo;
			}
			else if (arHai[i][0] == "furo")
			{
				sFuro += showFuroWithColor(arHai[i][1], showPlayer, nFuro);
				si += showFuroByAnim(arHai[i][1], showPlayer, nFuro, nFuroAnim, offsetX);
				nFuro++;
				nFuroAnim++;
				nFuroOffset += _strlen(arHai[i][1]) / 2;
				offsetX += 45 + (32 * (_strlen(arHai[i][1]) / 2 - 1));
				if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
					offsetX -= 32;
				nNormal -= 3;
			}
			else if (arHai[i][0] == "ankan")
			{
				sFuro += showAnkan(arHai[i][1]);
				si += showAnkanByAnim(arHai[i][1], showPlayer, nFuroAnim, offsetX);
				nFuroAnim++;
				nFuroOffset += _strlen(arHai[i][1]) / 2;
				offsetX += 32 * 4;
				nNormal -= 3;
			}
		}
		if (tsumoShowing != "")
		{
			s += " ";
			string haiCode;
			if (isOpen)
				haiCode += tsumoShowing;
			else
				haiCode += "tb";
			s += convertUnicode(haiCode);
			animNo = "" + (showPlayer + 1) + "0" + fillzero(14) + "0" + getSurfaceCode(haiCode);
			savedTehai += animNo;
			int offset = 16 - ((13 - nNormal) * 32);
			si += getFuroAnimOffset(showPlayer, animNo, offset, 0);
		}
		if (sFuro != "")
			s += "\_l[" + (260 - (15 * nFuroOffset)) + ",]" + sFuro;
		for (int i = 0; savedTehai[i] != nil; i++)
		{
			arSavedTehai[showPlayer] += savedTehai[i];
		}
	}
	else
	{
		savedTsumo = tsumoShowing;
		string sFuro;
		int nFuro;
		int nFuroAnim;
		int nFuroOffset;
		int nNormal = 13;
		int offsetX = 32;
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (arHai[i][0] == "furo")
			{
				if (_strlen(arHai[i][1]) == 6)
				{
					offsetX -= 45 + (32 * 2);
				}
				else
				{
					offsetX -= 45 + (32 * 3);
					if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
						offsetX += 32;
				}
			}
			else if (arHai[i][0] == "ankan")
			{
				offsetX -= 32 * 4;
			}
		}
		array arKuikae = {};//喰い替え対象
		if (_strlen(openedHai) == 4)
		{
			int n1 = _substr(openedHai, 0, 1);
			int n2 = _substr(openedHai, 2, 1);
			string color = _substr(openedHai, 1, 1);
			if (n1 + 1 == n2)//チーで両面
			{
				if (n1 > 1)
					arKuikae += "" + (n1 - 1) + color;
				if (n2 < 9)
					arKuikae += "" + (n2 + 1) + color;
			}
			else if (n1 + 2 == n2)//チーで嵌張
			{
				arKuikae += "" + (n1 + 1) + color;
			}
			else if (n1 == n2)//ポン
			{
				arKuikae += "" + n1 + color;
			}
		}
		for (int i = 0; arHai[i] != nil; i++)
		{
			if (tsumoShowing != "")
			{
				if (arHai[i][0] == "normal")
				{
					if (arRichiJunme[3] == -1)
					{
						s += "\__q[Menu_Sutehai," + arHai[i][1] + "," + tsumoShowing + "]" + convertUnicode(arHai[i][1]) + "\__q";
						animNo = "40" + fillzero(i + 1) + "0" + getSurfaceCode(arHai[i][1]);
						si += "\i[" + animNo + "]";
						savedTehai += animNo;
						animNo = "40" + fillzero(i + 1) + "999";
						si += "\i[" + animNo + "]";
					}
					else if (arRichiJunme[3] == _aryvn(arKawa[3]))
					{
						int shanten = getShanten(removeHai(addHai(arTehai[3], tsumoShowing), arHai[i][1]));
						if (shanten == 0)
						{
							s += "\__q[Menu_Sutehai," + arHai[i][1] + "," + tsumoShowing + "]" + convertUnicode(arHai[i][1]) + "\__q";
							animNo = "40" + fillzero(i + 1) + "0" + getSurfaceCode(arHai[i][1]);
							si += "\i[" + animNo + "]";
							savedTehai += animNo;
							animNo = "40" + fillzero(i + 1) + "999";
							si += "\i[" + animNo + "]";
						}
						else
						{
							s += "\f[color,disable]" + convertUnicode(arHai[i][1]) + "\f[color,default]";
							animNo = "40" + fillzero(i + 1) + "0" + ((int)getSurfaceCode(arHai[i][1]) + 50);
							si += "\i[" + animNo + "]";
							savedTehai += animNo;
						}
					}
					else
					{
						s += convertUnicode(arHai[i][1]);
						animNo = "40" + fillzero(i + 1) + "0" + getSurfaceCode(arHai[i][1]);
						si += "\i[" + animNo + "]";
						savedTehai += animNo;
					}
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], showPlayer, nFuro);
					si += showFuroByAnim(arHai[i][1], showPlayer, nFuro, nFuroAnim, offsetX);
					nFuro++;
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 45 + (32 * (_strlen(arHai[i][1]) / 2 - 1));
					if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
						offsetX -= 32;
					nNormal -= 3;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					si += showAnkanByAnim(arHai[i][1], showPlayer, nFuroAnim, offsetX);
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 32 * 4;
					nNormal -= 3;
				}
			}
			else if (openedHai != "" && showPlayer == nPlayer && tsumoShowing == "")
			{
				if (arHai[i][0] == "normal")
				{
					if (ASEARCH(arHai[i][1], arKuikae) >= 0)
					{
						s += "\f[color,disable]" + convertUnicode(arHai[i][1]) + "\f[color,default]";
						animNo = "40" + fillzero(i + 1) + "0" + ((int)getSurfaceCode(arHai[i][1]) + 50);
						si += "\i[" + animNo + "]";
						savedTehai += animNo;
					}
					else
					{
						s += "\__q[Menu_Sutehai," + arHai[i][1] + ",]" + convertUnicode(arHai[i][1]) + "\__q";
						animNo = "40" + fillzero(i + 1) + "0" + getSurfaceCode(arHai[i][1]);
						si += "\i[" + animNo + "]";
						savedTehai += animNo;
						animNo = "40" + fillzero(i + 1) + "999";
						si += "\i[" + animNo + "]";
					}
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], showPlayer, nFuro);
					si += showFuroByAnim(arHai[i][1], showPlayer, nFuro, nFuroAnim, offsetX);
					nFuro++;
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 45 + (32 * (_strlen(arHai[i][1]) / 2 - 1));
					if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
						offsetX -= 32;
					nNormal -= 3;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					si += showAnkanByAnim(arHai[i][1], showPlayer, nFuroAnim, offsetX);
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 32 * 4;
					nNormal -= 3;
				}
			}
			else
			{
				if (arHai[i][0] == "normal")
				{
					s += convertUnicode(arHai[i][1]);
					animNo = "40" + fillzero(i + 1) + "0" + getSurfaceCode(arHai[i][1]);
					si += "\i[" + animNo + "]";
					savedTehai += animNo;
				}
				else if (arHai[i][0] == "furo")
				{
					sFuro += showFuroWithColor(arHai[i][1], showPlayer, nFuro);
					si += showFuroByAnim(arHai[i][1], showPlayer, nFuro, nFuroAnim, offsetX);
					nFuro++;
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 45 + (32 * (_strlen(arHai[i][1]) / 2 - 1));
					if (ASEARCH(_substr(arHai[i][1], 0, 2), arKakanHistory[showPlayer]) >= 0)
						offsetX -= 32;
					nNormal -= 3;
				}
				else if (arHai[i][0] == "ankan")
				{
					sFuro += showAnkan(arHai[i][1]);
					si += showAnkanByAnim(arHai[i][1], showPlayer, nFuroAnim, offsetX);
					nFuroAnim++;
					nFuroOffset += _strlen(arHai[i][1]) / 2;
					offsetX += 32 * 4;
					nNormal -= 3;
				}
			}
		}
		if (tsumoShowing != "")
		{
			s += " ";
			if (arRichiJunme[3] == _aryvn(arKawa[3]))
			{
				int shanten = getShanten(arTehai[3]);
				if (shanten == 0)
				{
					s += "\__q[Menu_Sutehai," + tsumoShowing + "," + tsumoShowing + "]" + convertUnicode(tsumoShowing) + "\__q";					
					animNo = "40" + fillzero(14) + "0" + getSurfaceCode(tsumoShowing);
					savedTehai += animNo;
					si += "\![anim,offset," + animNo + "," + (16 - ((13 - nNormal) * 32)) + ",0]";
					si += "\i[" + animNo + "]";
					animNo = "40" + fillzero(14) + "999";
					si += "\![anim,offset," + animNo + "," + (16 - ((13 - nNormal) * 32)) + ",0]";
					si += "\i[" + animNo + "]";
				}
				else
				{
					s += "\f[color,disable]" + convertUnicode(tsumoShowing) + "\f[color,default]";
					animNo = "40" + fillzero(14) + "0" + ((int)getSurfaceCode(tsumoShowing) + 50);
					savedTehai += animNo;
					si += "\![anim,offset," + animNo + "," + (16 - ((13 - nNormal) * 32)) + ",0]";
					si += "\i[" + animNo + "]";
				}
			}
			else
			{
				s += "\__q[Menu_Sutehai," + tsumoShowing + "," + tsumoShowing + "]" + convertUnicode(tsumoShowing) + "\__q";
				animNo = "40" + fillzero(14) + "0" + getSurfaceCode(tsumoShowing);
				savedTehai += animNo;
				si += "\![anim,offset," + animNo + "," + (16 - ((13 - nNormal) * 32)) + ",0]";
				si += "\i[" + animNo + "]";
				animNo = "40" + fillzero(14) + "999";
				si += "\![anim,offset," + animNo + "," + (16 - ((13 - nNormal) * 32)) + ",0]";
				si += "\i[" + animNo + "]";
			}
		}
		if (sFuro != "")
			s += "\_l[" + (260 - (15 * nFuroOffset)) + ",]" + sFuro;
		for (int i = 0; savedTehai[i] != nil; i++)
		{
			arSavedTehai[3] += savedTehai[i];
		}
	}
	if (modeEx)
		return si;
	else
		return s + si;
}

string fillzero(int n)
{
	if (n < 10)
		return "0" + n;
	else
		return "" + n;
}

string showFuroWithColor(string strFuro, int nPlayer, int nFuroIndex)
{
	int nFuroIndexRev = (_aryvn(arFuroHistory[nPlayer]) - 1) - nFuroIndex;
	string strFuroHai = arFuroHistory[nPlayer][nFuroIndexRev][0];
	int nSutePlayer = arFuroHistory[nPlayer][nFuroIndexRev][1];
	strFuro = strFuroHai + removeHai(strFuro, strFuroHai);
	string s;
	if (_strlen(strFuro) == 6)
	{
		if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 0, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 0, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 2, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 2, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 4, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 4, 2));
	}
	else if (_strlen(strFuro) == 8)
	{
		if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 0, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 0, 2));
		s += convertUnicode(_substr(strFuro, 2, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 4, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 4, 2));
		if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			s += "\f[color,blue]" + convertUnicode(_substr(strFuro, 6, 2)) + "\f[color,default]";
		else
			s += convertUnicode(_substr(strFuro, 6, 2));
	}
	return s;
}

string showFuroByAnim(string strFuro, int nPlayer, int nFuroIndex, int nFuroAminIndex, int offsetX)
{
	int nFuroIndexRev = (_aryvn(arFuroHistory[nPlayer]) - 1) - nFuroIndex;
	string strFuroHai = arFuroHistory[nPlayer][nFuroIndexRev][0];
	int nSutePlayer = arFuroHistory[nPlayer][nFuroIndexRev][1];
	strFuro = strFuroHai + removeHai(strFuro, strFuroHai);
	array savedTehai = {};
	string s;
	if (_strlen(strFuro) == 6)
	{
		string animNo;
		if ((4 + nPlayer - nSutePlayer) % 4 == 1)
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "50" + getSurfaceCode(_substr(strFuro, 0, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
			offsetX += 45;
		}
		else
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "10" + getSurfaceCode(_substr(strFuro, 0, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
			offsetX += 32;
		}
		if ((4 + nPlayer - nSutePlayer) % 4 == 2)
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "50" + getSurfaceCode(_substr(strFuro, 2, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
			offsetX += 45;
		}
		else
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "20" + getSurfaceCode(_substr(strFuro, 2, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
			offsetX += 32;
		}
		if ((4 + nPlayer - nSutePlayer) % 4 == 3)
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "50" + getSurfaceCode(_substr(strFuro, 4, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
		}
		else
		{
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "30" + getSurfaceCode(_substr(strFuro, 4, 2));
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
		}
	}
	else if (_strlen(strFuro) == 8)
	{
		string animNo;
		string hai = _substr(strFuro, 0, 2);
		string haiCode = "0" + getSurfaceCode(hai);
		if (ASEARCH(hai, arKakanHistory[nPlayer]) >= 0)
		{
			if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "6" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				offsetX += 45;
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "1" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
				offsetX += 32;
			}
			if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "6" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				offsetX += 45;
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "2" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
				offsetX += 32;
			}
			if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "6" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "3" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
			}
		}
		else
		{
			if ((4 + nPlayer - nSutePlayer) % 4 == 1)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				offsetX += 45;
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "1" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
				offsetX += 32;
			}
			animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "2" + haiCode;
			savedTehai += animNo;
			s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
			offsetX += 32;
			if ((4 + nPlayer - nSutePlayer) % 4 == 2)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
				offsetX += 45;
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "3" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
				offsetX += 32;
			}
			if ((4 + nPlayer - nSutePlayer) % 4 == 3)
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "5" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 13);
			}
			else
			{
				animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAminIndex) + "4" + haiCode;
				savedTehai += animNo;
				s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
			}
		}
	}
	for (int i = 0; savedTehai[i] != nil; i++)
	{
		arSavedTehai[nPlayer] += savedTehai[i];
	}
	return s;
}

string getFuroAnimOffset(int nPlayer, string animNo, int offset, int size)
{
	string s;
	if (nPlayer == 3)
		s = "\![anim,offset," + animNo + "," + offset + ",0]\i[" + animNo + "]";
	else if (nPlayer == 0)
		s = "\![anim,offset," + animNo + ",0," + (-1 * (offset + size)) + "]\i[" + animNo + "]";
	else if (nPlayer == 1)
		s = "\![anim,offset," + animNo + "," + (-1 * (offset + size)) + ",0]\i[" + animNo + "]";
	else if (nPlayer == 2)
		s = "\![anim,offset," + animNo + ",0," + offset + "]\i[" + animNo + "]";
	return s;
}

string showAnkan(string strAnkan)
{
	string hai = _substr(strAnkan, 0, 2);
	return convertUnicode("tb") + convertUnicode(hai) + convertUnicode(hai) + convertUnicode("tb");
}

string showAnkanByAnim(string strAnkan, int nPlayer, int nFuroAnimIndex, int offsetX)
{
	string s;
	string animNo;
	array savedTehai = {};
	string hai = _substr(strAnkan, 0, 2);
	animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAnimIndex) + "10" + getSurfaceCode("tb");
	savedTehai += animNo;
	s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
	offsetX += 32;
	animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAnimIndex) + "20" + getSurfaceCode(hai);
	savedTehai += animNo;
	s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
	offsetX += 32;
	animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAnimIndex) + "30" + getSurfaceCode(hai);
	savedTehai += animNo;
	s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
	offsetX += 32;
	animNo = "" + (nPlayer + 1) + "0" + (2 + nFuroAnimIndex) + "40" + getSurfaceCode("tb");
	savedTehai += animNo;
	s += getFuroAnimOffset(nPlayer, animNo, offsetX, 0);
	for (int i = 0; savedTehai[i] != nil; i++)
	{
		arSavedTehai[nPlayer] += savedTehai[i];
	}
	return s;
}

string showKawa(int nPlayer, int p, int agariPlayer, int isFuroChance)
{
	string s;
	string si;
	array savedTehai = {};
	int n = _aryvn(arKawa[p]);
	for (int i = 0; i < n; i++)
	{
		string animNo = "" + (p + 1) + "1" + fillzero(i) + "0" + getSurfaceCode(arKawa[p][i]);
		if (isFuroChance && i == n - 1)
		{
			if (i == arRichiJunme[p])
			{
				s += "\f[color,red]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
				animNo = "" + (p + 1) + "2" + fillzero(i) + "" + ((int)getSurfaceCode(arKawa[p][i]) + 100);
			}
			else
			{
				s += convertUnicode(arKawa[p][i]);
				animNo = "" + (p + 1) + "1" + fillzero(i) + "" + ((int)getSurfaceCode(arKawa[p][i]) + 100);
			}
		}
		else if ((i == arRichiJunme[p]) && (ASEARCH(i, arFuroJunme[p]) >= 0))
		{
			s += "\f[color,red]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			animNo = "" + (p + 1) + "2" + fillzero(i) + "0" + ((int)getSurfaceCode(arKawa[p][i]) + 50);
		}
		else if (i == arRichiJunme[p])
		{
			s += "\f[color,red]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			animNo = "" + (p + 1) + "2" + fillzero(i) + "0" + getSurfaceCode(arKawa[p][i]);
		}
		else if (ASEARCH(i, arFuroJunme[p]) >= 0)
		{
			s += "\f[color,disable]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			animNo = "" + (p + 1) + "1" + fillzero(i) + "0" + ((int)getSurfaceCode(arKawa[p][i]) + 50);
		}
		else if (agariPlayer >= 0 && p != agariPlayer && p == nPlayer && i == _aryvn(arKawa[p]) - 1)
		{
			s += "\f[color,purple]" + convertUnicode(arKawa[p][i]) + "\f[color,default]";
			if (i == arRichiJunme[p])
				animNo = "" + (p + 1) + "2" + fillzero(i) + "" + ((int)getSurfaceCode(arKawa[p][i]) + 200);
			else
				animNo = "" + (p + 1) + "1" + fillzero(i) + "" + ((int)getSurfaceCode(arKawa[p][i]) + 200);
		}
		else
		{
			s += convertUnicode(arKawa[p][i]);
		}
		if (arRichiJunme[p] >= 0 && i > arRichiJunme[p] && (int)(i / 6) == (int)(arRichiJunme[p] / 6))
		{
			int offset = 13;
			if (p == 0)
				si += "\![anim,offset," + animNo + ",0,-" + offset + "]";
			else if (p == 1)
				si += "\![anim,offset," + animNo + ",-" + offset + ",0]";
			else if (p == 2)
				si += "\![anim,offset," + animNo + ",0," + offset + "]";
			else if (p == 3)
				si += "\![anim,offset," + animNo + "," + offset + ",0]";
		}
		savedTehai += animNo;
		si += "\i[" + animNo + "]";
	}
	for (int i = 0; savedTehai[i] != nil; i++)
	{
		arSavedTehai[p] += savedTehai[i];
	}
	if (modeEx)
		return si;
	else
		return s + si;
}

string addHai(string strHai1, string strHai2)
{
	array arHai2 = strHaiToArray(strHai2);
	string strToAdd = strHai1;
	for (int i = 0; arHai2[i] != nil; i++)
	{
		string strAdded = "";
		int isAdded = 0;
		for (int j = 0; j < _strlen(strToAdd); j++)
		{
			if (_substr(strToAdd, j, 1) == "<")
			{
				if (!isAdded)
				{
					isAdded = 1;
					strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
				}
				if (_substr(strToAdd, j + 7, 1) == ">")
				{
					strAdded += _substr(strToAdd, j, 8);
					j += 7;
				}
				else if (_substr(strToAdd, j + 9, 1) == ">")
				{
					strAdded += _substr(strToAdd, j, 10);
					j += 9;
				}
			}
			else if (_substr(strToAdd, j, 1) == "(")
			{
				if (!isAdded)
				{
					isAdded = 1;
					strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
				}
				if (_substr(strToAdd, j + 9, 1) == ")")
				{
					strAdded += _substr(strToAdd, j, 10);
					j += 9;
				}
			}
			else
			{
				strAdded += _substr(strToAdd, j, 2);
				j++;
			}
		}
		if (!isAdded)
		{
			strAdded = _arystr(sortHai(strHaiToArray(strAdded + arHai2[i])));
		}
		strToAdd = strAdded;
	}
	return strToAdd;
}

string removeHai(string strHai1, string strHai2)
{
	array arHai2 = strHaiToArray(strHai2);
	string strToRemove = strHai1;
	for (int i = 0; arHai2[i] != nil; i++)
	{
		string strRemoved = "";
		int isRemoved = 0;
		for (int j = 0; j < _strlen(strToRemove); j++)
		{
			if (_substr(strToRemove, j, 1) == "<")
			{
				if (_substr(strToRemove, j + 7, 1) == ">")
				{
					strRemoved += _substr(strToRemove, j, 8);
					j += 7;
				}
				else if (_substr(strToRemove, j + 9, 1) == ">")
				{
					strRemoved += _substr(strToRemove, j, 10);
					j += 9;
				}
			}
			else if (_substr(strToRemove, j, 1) == "(")
			{
				if (_substr(strToRemove, j + 9, 1) == ")")
				{
					strRemoved += _substr(strToRemove, j, 10);
					j += 9;
				}
			}
			else
			{
				if (!isRemoved && _substr(strToRemove, j, 2) == arHai2[i])
				{
					isRemoved = 1;
				}
				else
				{
					strRemoved += _substr(strToRemove, j, 2);
				}
				j++;
			}
		}
		strToRemove = strRemoved;
	}
	return strToRemove;
}

array removeElementByName(array ary, string name, int count)
{
	array arRet;
	int n = 0;
	for (int i = 0; ary[i] != nil; i++)
	{
		if (ary[i] == name && n < count)
		{
			n++;
		}
		else
		{
			arRet += ary[i];
		}
	}
	return arRet;
}

array strHaiToArray(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		arRet += _substr(strHai, i, 2);
		i++;
	}
	return arRet;
}

array strHaiToArrayWithoutFuro(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		if (_substr(strHai, i, 1) == "<" || _substr(strHai, i, 1) == "(")
			return arRet;
		arRet += _substr(strHai, i, 2);
		i++;
	}
	return arRet;
}

string removeFuro(string strHai)
{
	int n1 = _strstr(strHai, "<");
	int n2 = _strstr(strHai, "(");
	if (n1 == -1 && n2 == -1)
		return strHai;
	else if (n2 == -1)
		return _substr(strHai, 0, n1);
	else if (n1 == -1)
		return _substr(strHai, 0, n2);
	else if (n1 < n2)
		return _substr(strHai, 0, n1);
	else
		return _substr(strHai, 0, n2);
}

array strHaiToArrayWithFuro(string strHai)
{
	array arRet;
	for (int i = 0; i < _strlen(strHai); i++)
	{
		if (_substr(strHai, i, 1) == "<")
		{
			if (_substr(strHai, i + 7, 1) == ">")
			{
				arRet += {"furo", _substr(strHai, i + 1, 6)};
				i += 7;
			}
			else if (_substr(strHai, i + 9, 1) == ">")
			{
				arRet += {"furo", _substr(strHai, i + 1, 8)};
				i += 9;
			}
		}
		else if (_substr(strHai, i, 1) == "(")
		{
			if (_substr(strHai, i + 9, 1) == ")")
			{
				arRet += {"ankan", _substr(strHai, i + 1, 8)};
				i += 9;
			}
		}
		else
		{
			arRet += {"normal", _substr(strHai, i, 2)};
			i++;
		}
	}
	return arRet;
}

array shuffleArray(array a)
{
	array arRet;
	while (_aryvn(a) > 0)
	{
		int n = _aryvn(a);
		int index = _rand() * n / 10000;
		array an = {};
		for (int i = 0; i < n; i++)
		{
			if (i == index)
				arRet += a[i];
			else
				an += a[i];
		}
		a = an;
	}
	return arRet;
}

array getArrayRange(array a, int start, int end)
{
	array arRet;
	int x;
	int y;
	if (start <= end)
	{
		x = start;
		y = end;
	}
	else
	{
		x = end;
		y = start;
	}
	for (int i = x; i <= y; i++)
	{
		arRet += a[i];
	}
	return arRet;
}

array uniq(array ary)
{
	array a;
	for (int i = 0; ary[i] != nil; i++)
	{
		if (ASEARCH(ary[i], a) == -1)
			a += ary[i];
	}
	return a;
}

int ASEARCH(string key, array ary)
{
	for (int i = 0; ary[i] != nil; i++)
	{
		if (key == ary[i])
			return i;
	}
	return -1;
}

array ASEARCHEX(string key, array ary)
{
	array ret = {};
	for (int i = 0; ary[i] != nil; i++)
	{
		if (key == ary[i])
			ret += i;
	}
	return ret;
}

string JOIN(array a, string delim)
{
	if (_aryvn(a) < 1)
		return "";
	if (_aryvn(a) < 2)
		return a[0];
	string s = a[0];
	for (int i = 1; a[i] != nil; i++)
	{
		s += delim + a[i];
	}
	return s;
}

//ソート
array sortHai(array a)
{
	array arRet;
	//バブルソートでいいや…
	while (_aryvn(a) > 0)
	{
		int n = _aryvn(a);
		int index = -1;
		int minValue = 99;
		array an = {};
		for (int i = 0; i < n; i++)
		{
			int v = getSortCode(a[i]);
			if (v < minValue) {
				if (index >= 0)
					an += a[index];
				index = i;
				minValue = v;
			}
			else
			{
				an += a[i];
			}
		}
		arRet += a[index];
		a = an;
	}
	return arRet;
}

//\_u[0xXXXXX]形式に変換
string convertUnicode(string hai)
{
	dict d = ${
		$("1m", "1F007"),//一萬
		$("2m", "1F008"),
		$("3m", "1F009"),
		$("4m", "1F00A"),
		$("5m", "1F00B"),
		$("6m", "1F00C"),
		$("7m", "1F00D"),
		$("8m", "1F00E"),
		$("9m", "1F00F"),
		$("1p", "1F019"),//一筒
		$("2p", "1F01A"),
		$("3p", "1F01B"),
		$("4p", "1F01C"),
		$("5p", "1F01D"),
		$("6p", "1F01E"),
		$("7p", "1F01F"),
		$("8p", "1F020"),
		$("9p", "1F021"),
		$("1s", "1F010"),//一索
		$("2s", "1F011"),
		$("3s", "1F012"),
		$("4s", "1F013"),
		$("5s", "1F014"),
		$("6s", "1F015"),
		$("7s", "1F016"),
		$("8s", "1F017"),
		$("9s", "1F018"),
		$("1z", "1F000"),//東
		$("2z", "1F001"),//南
		$("3z", "1F002"),//西
		$("4z", "1F003"),//北
		$("5z", "1F006"),//白
		$("6z", "1F005"),//発
		$("7z", "1F004"),//中
		$("tb", "1F02B")//Tile Back
	};
	return "\_u[0x" + d[hai] + "]";
}

//ソート順定義
int getSortCode(string hai)
{
	string s = "1m2m3m4m5m6m7m8m9m1p2p3p4p5p6p7p8p9p1s2s3s4s5s6s7s8s9s1z2z3z4z5z6z7z";
	int index = _strstr(s, hai);
	int r = index / 2;
	return r;
}

//surfaces.txtの定義番号に変換
string getSurfaceCode(string hai)
{
	dict d = ${
		$("1m", "01"),//一萬
		$("2m", "02"),
		$("3m", "03"),
		$("4m", "04"),
		$("5m", "05"),
		$("6m", "06"),
		$("7m", "07"),
		$("8m", "08"),
		$("9m", "09"),
		$("1p", "11"),//一筒
		$("2p", "12"),
		$("3p", "13"),
		$("4p", "14"),
		$("5p", "15"),
		$("6p", "16"),
		$("7p", "17"),
		$("8p", "18"),
		$("9p", "19"),
		$("1s", "21"),//一索
		$("2s", "22"),
		$("3s", "23"),
		$("4s", "24"),
		$("5s", "25"),
		$("6s", "26"),
		$("7s", "27"),
		$("8s", "28"),
		$("9s", "29"),
		$("1z", "31"),//東
		$("2z", "32"),//南
		$("3z", "33"),//西
		$("4z", "34"),//北
		$("5z", "35"),//白
		$("6z", "36"),//発
		$("7z", "37"),//中
		$("tb", "00")//Tile Back
	};
	return d[hai];
}

string notifyByIndex(int index, array a)
{
	if (isSoloMode)
		return "";
	string s;
	if (useDSSTP)
	{
		string delim = _bytechar(2);
		s += "\![raise,OnDirectHandUtilCall," + playerName[index] + "," + JOIN(a, delim) + "]";
	}
	else
	{
		s += "\![raiseother," + playerName[index] + ",OnMahjong," + getVersion();
		for (int i = 0; a[i] != nil; i++)
		{
			s += "," + escapeText(a[i]);
		}
		s += "]";
	}
	if (_strstr(a[0], "?") >= 0)
	{
		dResponseNeed[playerName[index]] = a[0];
		isRecievedResponse = 0;
	}
	return s;
}

string notifyAll(array a)
{
	if (isSoloMode)
		return "";
	string s;
	if (useDSSTP)
	{
		string delim = _bytechar(2);
		for (int i = 0; i < 3; i++)
		{
			s += "\![raise,OnDirectHandUtilCall," + playerName[i] + "," + escapeText(JOIN(a, delim)) + "]";
		}
	}
	else
	{
		s += "\![raiseother," + playerName[0] + _bytechar(1) + playerName[1] + _bytechar(1) + playerName[2] + ",OnMahjong," + getVersion();
		for (int i = 0; a[i] != nil; i++)
		{
			s += "," + escapeText(a[i]);
		}
		s += "]";
	}
	if (a[0] == "say" && a[1] != "user")
	{
		switch (a[2])
		{
		case "chi":
		case "pon":
		case "kan":
			s += "\_w[2000]";
			break;
		case "tenpai":
		case "noten":
			s += "\_w[1000]";
			break;
		case "ron":
		case "tsumo":
		case "richi":
			break;
		default:
			break;
		}
	}
	return s;
}

string OnDirectHandUtilCall(dict ref)
{
	string delim = _bytechar(2);
	string name = ref["Reference0"];
	array a = _strsplit(ref["Reference1"], delim);
	dict d = notifyBySSTP(_getghosthwnd(name), a);
	if (_strstr(a[0], "?") >= 0)
	{
		redirectSSTPtoNotify(d, name);
	}
	return "";
}

string escapeText(string s)
{
	if (_regex_match(s,"^.*[,\"\[\]].*$"))
		return "\"" + _strreplace(s, "\"", "\"\"") + "\"";
	else
		return s;
}

dict notifyBySSTP(string strHwnd, array a)
{
	array arg = {"HandUtil", "DSSTPSend", strHwnd, "result"
		,"NOTIFY SSTP/1.1"
		,"Charset: Shift_JIS"
		,"Sender: " + _systemdict["OnNotifySelfInfo"]["Reference1"]
		,"Event: OnMahjong"
		,"Reference0: " + getVersion()};
	for (int i = 0; a[i] != nil; i++)
	{
		arg += "Reference" + (i + 1) + ": " + a[i];
	}
	return saorirequest2(arg);
}

dict saorirequest2(array a)
{
	switch (_aryvn(a))
	{
	case 1:
		return _saorirequest(a[0]);
	case 2:
		return _saorirequest(a[0], a[1]);
	case 3:
		return _saorirequest(a[0], a[1], a[2]);
	case 4:
		return _saorirequest(a[0], a[1], a[2], a[3]);
	case 5:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4]);
	case 6:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5]);
	case 7:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6]);
	case 8:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7]);
	case 9:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8]);
	case 10:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9]);
	case 11:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10]);
	case 12:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11]);
	case 13:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12]);
	case 14:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13]);
	case 15:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14]);
	case 16:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14], a[15]);
	case 17:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14], a[15], a[16]);
	case 18:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14], a[15], a[16], a[17]);
	case 19:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14], a[15], a[16], a[17], a[18]);
	case 20:
		return _saorirequest(a[0], a[1], a[2], a[3], a[4], a[5], a[6], a[7], a[8], a[9],
			a[10], a[11], a[12], a[13], a[14], a[15], a[16], a[17], a[18], a[19]);
	default:
		dict d;
		return d;
	}
}
